// Copyright (c) 2016 IBM Corporation.
/*
 * Generate C++ header file containing function and method signatures
 */
import std::core
import std::path
import std::string
import std::text
import std::text::Text4
import core::Core
import core::utils
import cg::cpp::cppcommon

import Systemdef

/* Generate C++ header file for module */
func CppSigs(#module: Module) -> Text4_text_sort
→ text⟦
#ifndef †⟨HeaderSigsGuard(GetModuleURL(#module))⟩
#define †⟨HeaderSigsGuard(GetModuleURL(#module))⟩
#include "ts.h"
#include "⟨STRING: Mangle(PathFileStem(GetModuleURL(#module)))⟩_types.h"
†⟨TextIf(HasExternFuncM(#module), ()->text⟦
#include "⟨STRING: Mangle(PathFileStem(GetModuleURL(#module)))⟩-extern.h"⟧)⟩

†⟨CppIncludeTypes(GetImports(GetModuleContent(#module)))⟩

using tosca::StringTerm;
using tosca::CStringTerm;
using tosca::DoubleTerm;
using tosca::MapTerm;

// Enumeration declarations
†⟨CppClassDecls(MapValues(GetDataSortsM(#module)))⟩

// Function forward declarations
†⟨CppForwardDecls (MapValues(GetFuncSortsM(#module)))⟩

/* @Brief Initialize module. Register data symbols and variable types */
void InitModule_⟨STRING: Mangle(PathFileStem(GetModuleURL(#module)))⟩(tosca::Context& context);
#endif⟧

/* Generate C++ header guard */
func HeaderSigsGuard(#url: String) -> Text4_text_sort
→ text⟦⟨STRING: Mangle(UpCase(PathFileStem(#url)))⟩_SIGS_H_⟧

/* Generate C++ header inclusion declarations */
func CppIncludeTypes(#imports: List<Core_cqidentifier_sort>) -> Text4_text_sort
→ TextFoldES(Map((imp) -> text⟦
#include "⟨STRING: JoinQIdentifier(imp, "/", TRUE)⟩_types.h"⟧, #imports))

//--- Class declaration

/* Generate class and method signatures for data sorts */
func CppClassDecls(#datasorts: List<Core_cdecl_sort>) -> Text4_text_sort
→ TextFoldES(Map((decl) -> CppClassDecl(decl), #datasorts))

func CppClassDecl(Core_cdecl_sort) -> Text4_text_sort

  rule CppClassDecl(cdecl⟦ data ##csortvars? ##CONSTRUCTOR ( ##cform* ) ⟧)
  → text⟦
    †⟨TextMapFoldES((cform) -> CppMethodNew(#CONSTRUCTOR, #csortvars?, cform, FALSE), #cform*)⟩
    †⟨CppTemplatePrefix(#csortvars?)⟩
    class †⟨SortNameToCppClassName(#CONSTRUCTOR)⟩ : public tosca::Term
    {
    public:
      †⟨TextMapFoldES((cform) -> CppMethodAs(#CONSTRUCTOR, #csortvars?, cform, FALSE), #cform*)⟩
      †⟨TextIf(IsAllowsVarForms(#cform*), ()->CppMethodGetVariable(#CONSTRUCTOR, #csortvars?, FALSE))⟩
    };
    †⟨TextMapFoldES((cform) -> CppFormClassDecl(#CONSTRUCTOR, #csortvars?, cform), #cform*)⟩
    ⟧

  rule CppClassDecl(#cdecl)
  → text⟦⟧

/* Generate class and method signatures for the given data form */
func CppFormClassDecl(String, List<Core_csortvars_sort>, Core_cform_sort) -> Text4_text_sort

  rule CppFormClassDecl(#sortname, #csortvars?, cform⟦ ##CONSTRUCTOR ##csorts? ⟧)
  → text⟦
      †⟨CppTemplatePrefix(#csortvars?)⟩
      class †⟨FormCppClassName(#CONSTRUCTOR)⟩: public †⟨SortNameToCppClassName(#sortname)⟩†⟨MaybeCppTypeArguments(#csortvars?)⟩
      {
      public:
        †⟨CppFormConstructor(#sortname, #csortvars?, #CONSTRUCTOR, #csorts?, FALSE)⟩
        †⟨CppFormMethodMake(#sortname, #csortvars?, #CONSTRUCTOR, #csorts?, FALSE)⟩
        †⟨CppFormMethodAs(#sortname, #csortvars?, #CONSTRUCTOR, FALSE)⟩
        †⟨CppFormGetValue(#sortname, #csortvars?, #CONSTRUCTOR, #csorts?, FALSE)⟩
        †⟨CppFormGetSub(#sortname, #csortvars?, #CONSTRUCTOR, #csorts?, FALSE)⟩
        †⟨CppFormSetSub(#sortname, #csortvars?, #CONSTRUCTOR, #csorts?, FALSE)⟩
        †⟨CppFormGetBinder(#sortname, #csortvars?, #CONSTRUCTOR, #csorts?, FALSE)⟩
        †⟨CppFormSetBinder(#sortname, #csortvars?, #CONSTRUCTOR, #csorts?, FALSE)⟩

      protected:
        †⟨MaybeCppClassFields(#csorts?, (isvar type index subindex)->CppDeclField(isvar, type, index, subindex))⟩
        †⟨CppFormDefaultConstructor(#sortname, #csortvars?, #CONSTRUCTOR, #csorts?, FALSE)⟩
      };⟧

  rule CppFormClassDecl(#sortname, #csortvars?, cform⟦ allows-variable ⟧)
  → text⟦
      †⟨CppTemplatePrefix(#csortvars?)⟩
      class †⟨FormCppClassName(#sortname)⟩Var: public tosca::Variable
      {
        public:
          †⟨CppVarConstructorVar(#sortname, #csortvars?, FALSE)⟩
          †⟨CppVarMethodUse(#sortname, #csortvars?, FALSE)⟩
          †⟨CppVarMethodGUse(#sortname, #csortvars?, FALSE)⟩
      };

      †⟨CppTemplatePrefix(#csortvars?)⟩
      class †⟨FormCppClassName(#sortname)⟩Use: public †⟨SortNameToCppClassName(#sortname)⟩†⟨MaybeCppTypeArguments(#csortvars?)⟩, public tosca::VariableUse
      {
        public:
          †⟨CppVarConstructorUse(#sortname, #csortvars?, FALSE)⟩
          †⟨CppVarMethodGetVariable(#sortname, #csortvars?, FALSE)⟩
          †⟨CppVarMethodGetGVariable(#sortname, #csortvars?, FALSE)⟩
      };

      namespace std {
          template<>
          struct hash<†⟨SortNameToCppClassName(#sortname)⟩*>
          {
          public:
            size_t operator()(const †⟨SortNameToCppClassName(#sortname)⟩* term) const
            {
              return std::hash<tosca::Term*>{}(term);
            }
          };

          template<>
          struct equal_to<†⟨SortNameToCppClassName(#sortname)⟩*>
          {
          public:
            bool operator()(const †⟨SortNameToCppClassName(#sortname)⟩* lhs, const †⟨SortNameToCppClassName(#sortname)⟩* rhs) const
            {
                return std::equal_to<tosca::Term*>{}(lhs, rhs);
            }
          };
      }

      ⟧

// --------- Form to class fields

/* Field declaration helper */
func CppDeclField(Bool, Text4_text_sort, Numeric, Numeric) -> Text4_text_sort
rule CppDeclField(FALSE, #type, #index, #subindex) → text⟦†⟨#type⟩* †⟨FieldName(#index)⟩;⟧
rule CppDeclField(TRUE, #type, #index, #subindex)  → text⟦†⟨#type⟩* †⟨VarFieldName(#index, #subindex)⟩;⟧

// --------- Global constants declarations for data forms

/* Generate class and method signatures for data sorts */
func CppGlobalData(#datasorts: List<Core_cdecl_sort>) -> Text4_text_sort
→ TextFoldES(Map((decl) -> CppGlobalDataDecl(decl), #datasorts))

func CppGlobalDataDecl(Core_cdecl_sort) -> Text4_text_sort
rule CppGlobalDataDecl(cdecl⟦ data ##CONSTRUCTOR ( ##cform* ) ⟧) → TextFoldES(Map((form) -> CppGlobalDataForm(#CONSTRUCTOR, form), #cform*))
rule CppGlobalDataDecl(#cdecl)                                   → text⟦⟧

func CppGlobalDataForm(String, Core_cform_sort) -> Text4_text_sort

  rule CppGlobalDataForm(#sortname, cform⟦ ##CONSTRUCTOR ##csorts? ⟧)
  → text⟦
       extern const CStringTerm& GC†⟨STRING: Mangle(#CONSTRUCTOR)⟩;⟧

  rule CppGlobalDataForm(#sortname, cform⟦ allows-variable ⟧)
  → text⟦
       extern const CStringTerm& GT†⟨STRING: Mangle(#sortname)⟩;⟧
