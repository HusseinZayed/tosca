// Copyright (c) 2016 IBM Corporation.
/*
 * Generate C++ header file
 */
import std::core
import std::path
import std::string
import std::mapdef
import std::listdef
import std::text
import std::text::Text4

import core::Core
import core::utils
import cg::cpp::cppcommon
import cg::cpp::cppenv

import systemdef

/* Generate C++ header file for module */
func CppHeader(#module: Module) -> Text4_text_sort
→ Debug("Generate C++ header file", ()->text⟦
#ifndef †⟨HeaderGuard(GetModuleURL(#module))⟩
#define †⟨HeaderGuard(GetModuleURL(#module))⟩
#include "ts.h"
#include "⟨STRING: Mangle(PathFileStem(GetModuleURL(#module)))⟩_sigs.h"

// Imports class declarations and signatures
†⟨CppIncludeSigs(GetImports(GetModuleContent(#module)))⟩

using tosca::StringTerm;
using tosca::DoubleTerm;
using tosca::MapTerm;
using tosca::Term;

// Templated method definitions
†⟨CppMethodDefs(MapValues(GetDataSortsC(GetModuleContent(#module))))⟩

// Templated function definitions
†⟨CppFunctionDefs(SetContent(CppNewEnv, GetModuleContent(#module)))⟩
#endif⟧)

/* Generate C++ header guard */
func HeaderGuard(#url: String) -> Text4_text_sort
→ text⟦⟨STRING: Mangle(UpCase(PathFileStem(#url)))⟩_H_⟧

/* Generate C++ header inclusion declarations */
func CppIncludeSigs(#imports: List<Core_cqidentifier_sort>) -> Text4_text_sort
→ TextFoldES(Map((imp) -> text⟦
#include "⟨STRING: JoinQIdentifier(imp, "/", TRUE)⟩_sigs.h"⟧, #imports))

// --------- Templated method definitions

func CppMethodDefs(#datasorts: List<Core_cdecl_sort>) -> Text4_text_sort
→ TextFoldES(Map((decl) -> CppMethodDef(decl), #datasorts))

func CppMethodDef(Core_cdecl_sort) -> Text4_text_sort

  rule CppMethodDef(cdecl⟦ data ##csortvars ##CONSTRUCTOR ( ##cform* ) ⟧)
  → let #allowvar = IsAllowsVarForms(#cform*)
    text⟦†⟨TextMapFoldES((cform) -> CppMethodNew(#CONSTRUCTOR, csortvars?⟦ ⟨csortvars: #csortvars⟩ ⟧, cform, TRUE), #cform*)⟩
†⟨TextMapFoldES((cform) -> CppMethodAs(#CONSTRUCTOR, csortvars?⟦ ⟨csortvars: #csortvars⟩ ⟧, cform, TRUE), #cform*)⟩
†⟨CppMethodMakeVariable(#CONSTRUCTOR, csortvars?⟦ ⟨csortvars: #csortvars⟩ ⟧, #allowvar, TRUE) /* TODO: consider generating only when allow variables */ ⟩
†⟨TextIf(#allowvar, ()->CppMethodGetVariable(#CONSTRUCTOR, csortvars?⟦ ⟨csortvars: #csortvars⟩ ⟧, TRUE))⟩
†⟨CppMethodMakeTerm(#CONSTRUCTOR, csortvars?⟦ ⟨csortvars: #csortvars⟩ ⟧, #cform*, TRUE)⟩
†⟨TextMapFoldES((cform) -> CppFormMethod(#CONSTRUCTOR, csortvars?⟦ ⟨csortvars: #csortvars⟩ ⟧, cform), #cform*)⟩⟧

  rule CppMethodDef(#cdecl)
  → text⟦⟧

/* Generate method defs for the given data form */
func CppFormMethod(String, List<Core_csortvars_sort>, Core_cform_sort) -> Text4_text_sort

  rule CppFormMethod(#sortname, #csortvars?, cform⟦ ##CONSTRUCTOR ##csortsdefault? ⟧)
  → let #sorts? = MaybeSortsDefaultToMaybeSorts(#csortsdefault?)
    text⟦†⟨CppFormConstructor(#sortname, #csortvars?, #CONSTRUCTOR, #sorts?, TRUE)⟩
†⟨CppFormDefaultConstructor(#sortname, #csortvars?, #CONSTRUCTOR, #sorts?, TRUE)⟩
†⟨CppFormMethodSymbol(#sortname, #csortvars?, #CONSTRUCTOR, TRUE)⟩
†⟨CppFormMethodMake(#sortname, #csortvars?, #CONSTRUCTOR, #sorts?, TRUE)⟩
†⟨CppFormMethodCopy(#sortname, #csortvars?, #CONSTRUCTOR, TRUE)⟩
†⟨CppFormMethodAs(#sortname, #csortvars?, #CONSTRUCTOR, TRUE)⟩
†⟨CppFormGetValue(#sortname, #csortvars?, #CONSTRUCTOR, #sorts?, TRUE)⟩
†⟨CppFormGetSub(#sortname, #csortvars?, #CONSTRUCTOR, #sorts?, TRUE)⟩
†⟨CppFormSetSub(#sortname, #csortvars?, #CONSTRUCTOR, #sorts?, TRUE)⟩
†⟨CppFormGetBinder(#sortname, #csortvars?, #CONSTRUCTOR, #sorts?, TRUE)⟩
†⟨CppFormSetBinder(#sortname, #csortvars?, #CONSTRUCTOR, #sorts?, TRUE)⟩
†⟨CppFormMakeFree(#sortname, #csortvars?, #CONSTRUCTOR, #sorts?, TRUE)⟩
†⟨CppFormMakeBound(#sortname, #csortvars?, #CONSTRUCTOR, #sorts?, TRUE)⟩
†⟨CppFormMakeTerm(#sortname, #csortvars?, #CONSTRUCTOR, #sorts?, TRUE)⟩⟧

  rule CppFormMethod(#sortname, #csortvars?, cform⟦ allows-variable ⟧)
  → text⟦†⟨CppVarConstructorVar(#sortname, #csortvars?, TRUE)⟩
†⟨CppVarMethodUse(#sortname, #csortvars?, TRUE)⟩
†⟨CppVarMethodGUse(#sortname, #csortvars?, TRUE)⟩
†⟨CppVarMethodCopy(#sortname, #csortvars?, TRUE)⟩
†⟨CppVarConstructorUse(#sortname, #csortvars?, TRUE)⟩
†⟨CppVarMethodGetVariable(#sortname, #csortvars?, TRUE)⟩
†⟨CppVarMethodGetGVariable(#sortname, #csortvars?, TRUE)⟩⟧

// --------- Templated function definitions

func CppFunctionDefs(#env: {String : EnvEntry}) -> Text4_text_sort
→ TextMapFoldES((key) ->CppFunctionDecl(#env, key, TRUE), MapKeys(GetFuncSorts(GetContent(#env))))
