// Copyright (c) 2016 IBM Corporation.
/*
 * Generate C++ source file
 */
import std::core
import std::path
import std::listdef
import std::text
import std::mapdef
import std::text::Text4

import core::Core
import cg::cppcommon
import cg::cppenv
import systemdef

/* Generate C++ source file for module */
func CppSource(#module: Module) -> Text4_text_sort
→ text⟦#include "⟨STRING: PathFileStem(GetModuleURL(#module))⟩.h"
†⟨CppClassDefs(MapValues<String Core_cdecl_sort>(GetDataSortsC(GetModuleContent(#module))))⟩
†⟨CppFunctionDefs(SetContent(CppNewEnv, GetModuleContent(#module)))⟩⟧

// --------- Data sorts

/* Generate class definition and method body for data sorts */
func CppClassDefs(#datasorts: List<Core_cdecl_sort>) -> Text4_text_sort
→ TextMapFold<Core_cdecl_sort>((decl) -> CppClassDef(decl), #datasorts)

func CppClassDef(Core_cdecl_sort) -> Text4_text_sort
rule CppClassDef(cdecl⟦ data ##csortvars? ##CONSTRUCTOR ( ##cform* ) ⟧)
→ text⟦†⟨TextMapFold<Core_cform_sort>((cform) -> WhenNoParams(#csortvars?, ()->CppMethodNew(#CONSTRUCTOR, #csortvars?, cform, FALSE)), #cform*)⟩
†⟨TextMapFold<Core_cform_sort>((cform) -> WhenNoParams(#csortvars?, ()->CppMethodAs(#CONSTRUCTOR, #csortvars?, cform, FALSE)), #cform*)⟩
†⟨TextMapFold<Core_cform_sort>((cform) -> CppFormDefs(#CONSTRUCTOR, #csortvars?, cform), #cform*)⟩⟧


/* Generate class and method definitions for the given data form */
func CppFormDefs(String, List<Core_csortvars_sort>, Core_cform_sort) -> Text4_text_sort

  rule CppFormDefs(#sortname, #csortvars?, cform⟦ ##CONSTRUCTOR ##csorts? ⟧)
  → text⟦†⟨WhenNoParams(#csortvars?, ()->CppFormConstructor(#sortname, #csortvars?, #CONSTRUCTOR, #csorts?, FALSE))⟩
  †⟨WhenNoParams(#csortvars?, ()->CppFormMethodAs(#sortname, #csortvars?, #CONSTRUCTOR, FALSE))⟩
  †⟨WhenNoParams(#csortvars?, ()->CppFormGetValue(#sortname, #csortvars?, #CONSTRUCTOR, #csorts?, FALSE))⟩⟧
//†⟨WhenNoParams(#csortvars?, ()->CppFormMethodCopy(#sortname, #csortvars?, #CONSTRUCTOR, FALSE))⟩

  rule CppFormDefs(#sortname, #csortvars?, cform⟦ allows-variable ⟧)
  → text⟦/*TODO: allows-variable*/⟧

// --------- Function sorts and associated rules

/* Generate function definitions corresponding to function sorts. */
func CppFunctionDefs(#env: {String : EnvEntry}) -> Text4_text_sort
→ TextMapFold<String>((key) -> CppFunctionDecl(#env, key, FALSE), MapKeys<String Core_cdecl_sort>(GetFuncSorts(GetContent(#env))))


/* Apply function when the list of template parameters is empty */
func WhenNoParams(#csortvars?: List<Core_csortvars_sort>, #: ()->Text4_text_sort) -> Text4_text_sort
→  TextIf(IsEmpty<Core_csortvars_sort>(#csortvars?), #)
