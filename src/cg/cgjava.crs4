// Copyright (c) 2016 IBM Corporation.
/*
 * TransScript Java 8 backend code generator
 */


import Std::Core
import Std::String
import Std::Num
import Std::Text
import Std::Listdef
import Std::Pairdef
import Std::Mapdef
import Std::Language

import Core::Core
import Core::Utils
import Text::Text4
import Systemdef

// ===== API


/*
 * Generates Java code from Core TransScript
 * @param System The TransScript system
 */

func TransScriptToJava(TSystem) -> Text4_text_sort
rule TransScriptToJava(TSystem(#url:String, #modules:{String : Module}))
→ JavaUnits(#url, MapValues<String Module>(#modules))

// --- Generate code.

/*
 Each Crsx module corresponds to a single Java class.

 Each rule declaration corresponds to a single Java function.
*/

func JavaUnits(String, List<Module>) -> Text4_text_sort
rule JavaUnits(#url, #modules)
  → text⟦
/** Generated File */
†⟨PackageDeclaration⟩

import java.util.function.Function;

import org.transscript.runtime.Context;
import org.transscript.runtime.Term;
import org.transscript.runtime.Variable;
import org.transscript.runtime.VariableUse;
import org.transscript.runtime.LazyTerm;
import org.transscript.runtime.StringTerm;
import org.transscript.runtime.StringTerm.VarStringTerm;
import org.transscript.runtime.DoubleTerm;
import org.transscript.runtime.DoubleTerm.VarDoubleTerm;
import org.transscript.runtime.MapTerm;
import static org.transscript.runtime.Term.force;
import static org.transscript.runtime.Term.subst;
import static org.transscript.runtime.Functions.*;
import static org.transscript.runtime.StringTerm.lazyStringTerm;
import static org.transscript.runtime.StringTerm.lazyStringTermMaker;
import static org.transscript.runtime.StringTerm.stringTerm;
import static org.transscript.runtime.StringTerm.varStringTerm;
import static org.transscript.runtime.DoubleTerm.lazyDoubleTerm;
import static org.transscript.runtime.DoubleTerm.lazyDoubleTermMaker;
import static org.transscript.runtime.DoubleTerm.doubleTerm;
import static org.transscript.runtime.MapTerm.lazyMapTerm;
import static org.transscript.runtime.MapTerm.lazyMapTermMaker;
import static org.transscript.runtime.LazyTerm.thunk;

†⟨TextFold(Map<Module Text4_text_sort>((x) -> JavaUnit(#url, x), #modules), text⟦⟧)⟩
⟧

/* Generate class for the given module */
func JavaUnit(String, eager Module) -> Text4_text_sort
rule JavaUnit(#mainurl, Module(#url:String,
        Content::Loaded(LoadedContent::LoadedContent(#imports:List<Core_cqconstructor_sort>, #rules:{String:List<Core_cdecl_sort>}, #datasorts:{String:Core_cdecl_sort}, #funcsorts:{String:Core_cdecl_sort}))))
→ text⟦
†⟨TextFold(Map<Core_cqconstructor_sort Text4_text_sort>((x) -> JavaImport(x), #imports), text⟦⟧)⟩

@SuppressWarnings("unused")
†⟨JavaClassVisibility(#mainurl, #url)⟩ class ⟨STRING:ClassName(#url)⟩ {

  /* Typed enumerations */
  †⟨TextFold( Map<Core_cdecl_sort Text4_text_sort>((x) ->EnumToClass(x, ClassName(#url)), MapValues<String Core_cdecl_sort>(#datasorts)), text⟦⟧)⟩
  †⟨TextFold( Map<String Text4_text_sort>((x) ->JavaMethod(x, text⟦⟨STRING:ClassName(#url)⟩⟧, #rules, #datasorts, #funcsorts), MapKeys<String Core_cdecl_sort>(#funcsorts)), text⟦⟧)⟩
  †⟨InitModule(#datasorts, #funcsorts, #imports)⟩
}⟧

func JavaImport(Core_cqconstructor_sort) -> Text4_text_sort
rule JavaImport(#name)
→ text⟦
import static †⟨PathToImport(QConsToString(#name, "::"))⟩.*;⟧ // TODO: maybe use '.' separator

func JavaClassVisibility(String, String) -> Text4_text_sort
rule JavaClassVisibility(#mainurl, #url)
→ If<Text4_text_sort>(StringEqual(#mainurl, #url),()->text⟦public⟧,()->text⟦static⟧)

//-------------- Translate enumerations

/* Generate interfaces corresponding to the enum type */
func EnumToClass(Core_cdecl_sort, String) -> Text4_text_sort
rule EnumToClass(cdecl⟦ data ##csortvars? ##CONSTRUCTOR ( ##cform* ) ⟧, #classname)
→ text⟦

†⟨TextFold(Map<Core_cform_sort Text4_text_sort>((x) -> FormToMethodNew(#CONSTRUCTOR, #csortvars?, x), #cform*), text⟦⟧)⟩

static public †⟨SortParamToTypeParam(#csortvars?, TRUE)⟩ ⟨STRING: ToJavaClassName(#CONSTRUCTOR)⟩†⟨SortParamToTypeParam(#csortvars?, FALSE)⟩ lazy⟨STRING: ToJavaClassName(#CONSTRUCTOR)⟩(Function<Context, ⟨STRING: ToJavaClassName(#CONSTRUCTOR)⟩†⟨SortParamToTypeParam(#csortvars?, FALSE)⟩> function)
{
  return new Lazy⟨STRING: ToJavaClassName(#CONSTRUCTOR)⟩(function);
}

static public †⟨SortParamToTypeParam(#csortvars?, TRUE)⟩ ThunkMaker<⟨STRING: ToJavaClassName(#CONSTRUCTOR)⟩†⟨SortParamToTypeParam(#csortvars?, FALSE)⟩> lazy⟨STRING: ToJavaClassName(#CONSTRUCTOR)⟩Maker()
{
  return ⟨STRING: #classname⟩::lazy⟨STRING: ToJavaClassName(#CONSTRUCTOR)⟩;
}

public interface ⟨STRING: ToJavaClassName(#CONSTRUCTOR)⟩†⟨SortParamToTypeParam(#csortvars?, TRUE)⟩ extends Term
{
  †⟨TextFold(Map<Core_cform_sort Text4_text_sort>((x) -> FormToMethodAs(#CONSTRUCTOR, #csortvars?, x), #cform*), text⟦⟧)⟩
}

†⟨TextFold(Map<Core_cform_sort Text4_text_sort>((x) -> FormToImplValue(#CONSTRUCTOR, #csortvars?, x), #cform*), text⟦⟧)⟩

static class Lazy⟨STRING: ToJavaClassName(#CONSTRUCTOR)⟩†⟨SortParamToTypeParam(#csortvars?, TRUE)⟩ extends LazyTerm<⟨STRING: ToJavaClassName(#CONSTRUCTOR)⟩†⟨SortParamToTypeParam(#csortvars?, FALSE)⟩> implements ⟨STRING: ToJavaClassName(#CONSTRUCTOR)⟩†⟨SortParamToTypeParam(#csortvars?, FALSE)⟩
{

  protected Lazy⟨STRING: ToJavaClassName(#CONSTRUCTOR)⟩(⟨STRING: ToJavaClassName(#CONSTRUCTOR)⟩†⟨SortParamToTypeParam(#csortvars?, FALSE)⟩ value)
	{
		super(value);
	}

  public Lazy⟨STRING: ToJavaClassName(#CONSTRUCTOR)⟩(Function<Context, ⟨STRING: ToJavaClassName(#CONSTRUCTOR)⟩†⟨SortParamToTypeParam(#csortvars?, FALSE)⟩> function)
	{
		super(function);
	}

  †⟨TextFold(Map<Core_cform_sort Text4_text_sort>((x) -> FormToMethodMemoAs(#CONSTRUCTOR, #csortvars?, x), #cform*), text⟦⟧)⟩

  @Override
	public ⟨STRING: ToJavaClassName(#CONSTRUCTOR)⟩†⟨SortParamToTypeParam(#csortvars?, FALSE)⟩ copy(Context c)
	{
		return new Lazy⟨STRING: ToJavaClassName(#CONSTRUCTOR)⟩†⟨SortParamToTypeParam(#csortvars?, FALSE)⟩(value);
	}

}⟧

/* Generate a 'new' static method for each enum value */

// TODO: optimize constant value.
func FormToMethodNew(String, List<Core_csortvars_sort>, Core_cform_sort) -> Text4_text_sort
  rule FormToMethodNew(#constructor, #csortvars?, cform⟦ ##CONSTRUCTOR ##csorts? ⟧)
  → text⟦
    static public †⟨SortParamToTypeParam(#csortvars?, TRUE)⟩ ⟨STRING: ToJavaClassName(#constructor)⟩†⟨SortParamToTypeParam(#csortvars?, FALSE)⟩ ⟨STRING: ToJavaMethodName(#CONSTRUCTOR)⟩ (Context context†⟨MaybeSortsToFormalParams(#csorts?, text⟦, ⟧)⟩)
    {
      return new †⟨EValueClassName(#CONSTRUCTOR)⟩†⟨SortParamToTypeParam(#csortvars?, FALSE)⟩(†⟨MaybeSortsToArgs(#csorts?)⟩);
    }⟧

  rule FormToMethodNew(#constructor, #csortvars?, cform⟦ allows-variable ⟧)
  → text⟦
    static †⟨SortParamToTypeParam(#csortvars?, TRUE)⟩ Var⟨STRING: ToJavaClassName(#constructor)⟩†⟨SortParamToTypeParam(#csortvars?, FALSE)⟩ ⟨STRING: ToJavaMethodName(ConcatString("var", #constructor))⟩ (Context ctx, String hint)
    {
      return new Var⟨STRING: ToJavaClassName(#constructor)⟩†⟨SortParamToTypeParam(#csortvars?, FALSE)⟩(ctx.makeGlobalName(hint));
    }⟧

// --- Generate a 'as' method for each enum value

func FormToMethodAs(String, List<Core_csortvars_sort>, Core_cform_sort) -> Text4_text_sort
  rule FormToMethodAs(#enumname, #csortvars?, cform⟦ ##CONSTRUCTOR ##csorts? ⟧)
  → text⟦
    default †⟨EValueClassName(#CONSTRUCTOR)⟩†⟨SortParamToTypeParam(#csortvars?, FALSE)⟩ †⟨MethodNameAs(#CONSTRUCTOR)⟩(Context context)
    {
      return null;
    }⟧

  rule FormToMethodAs(#enumname, #csortvars?, cform⟦ allows-variable ⟧)
  → text⟦⟧

func FormToMethodMemoAs(String, List<Core_csortvars_sort>, Core_cform_sort) -> Text4_text_sort
  rule FormToMethodMemoAs(#enumname, #csortvars?, cform⟦ ##CONSTRUCTOR ##csorts? ⟧)
  → text⟦
   @Override
   final public †⟨EValueClassName(#CONSTRUCTOR)⟩†⟨SortParamToTypeParam(#csortvars?, FALSE)⟩ †⟨MethodNameAs(#CONSTRUCTOR)⟩(Context context)
   {
     return eval(context).†⟨MethodNameAs(#CONSTRUCTOR)⟩(context);
   }⟧

 rule FormToMethodMemoAs(#enumname, #csortvars?, cform⟦ allows-variable ⟧)
 → text⟦⟧

func MethodNameAs(String) -> Text4_text_sort
rule MethodNameAs(#formCons) → text⟦⟨STRING: ToJavaMethodName(ConcatString("as", #formCons))⟩⟧

// --- Generate an static class for the enumeration value

func FormToImplValue(String, List<Core_csortvars_sort>, Core_cform_sort) -> Text4_text_sort
  rule FormToImplValue(#typename, #csortvars?, cform⟦ ##CONSTRUCTOR ##csorts? ⟧)
  → text⟦
      public static class †⟨EValueClassName(#CONSTRUCTOR)⟩†⟨SortParamToTypeParam(#csortvars?, TRUE)⟩ implements ⟨STRING: ToJavaClassName(#typename)⟩†⟨SortParamToTypeParam(#csortvars?, FALSE)⟩
      {
          †⟨MaybeSortsToClassFields(#csorts?)⟩

          †⟨MaybeSortsToDefaultConstructor(#csorts?,  EValueClassName(#CONSTRUCTOR))⟩

          public †⟨EValueClassName(#CONSTRUCTOR)⟩(†⟨MaybeSortsToFormalParams(#csorts?, text⟦⟧)⟩)
          {
            †⟨MaybeSortsToInitFields(#csorts?)⟩
          }

          @Override
          public †⟨EValueClassName(#CONSTRUCTOR)⟩†⟨SortParamToTypeParam(#csortvars?, FALSE)⟩ †⟨MethodNameAs(#CONSTRUCTOR)⟩(Context context)
          {
            return this;
          }

          @Override
          public Term copy(Context c)
          {
            return new †⟨EValueClassName(#CONSTRUCTOR)⟩†⟨SortParamToTypeParam(#csortvars?, FALSE)⟩();
          }

          †⟨MaybeSortsToGetValue(#csorts?)⟩
          †⟨MaybeSortsToGetSubs(#csorts?)⟩
          †⟨MaybeSortsToSetSubs(#csorts?)⟩
          †⟨MaybeSortsToGetBinder(#csorts?)⟩
          †⟨MaybeSortsToSetBinder(#csorts?)⟩

      }⟧

  rule FormToImplValue(#typename, #csortvars?, cform⟦ allows-variable⟧)
  → text⟦
      static class Var⟨STRING: ToJavaClassName(#typename)⟩†⟨SortParamToTypeParam(#csortvars?, TRUE)⟩Use extends VariableUse implements ⟨STRING: ToJavaClassName(#typename)⟩†⟨SortParamToTypeParam(#csortvars?, FALSE)⟩
      {
        public Var⟨STRING: ToJavaClassName(#typename)⟩Use(Variable var)
        {
          super(var);
        }
    }

    public static class Var⟨STRING: ToJavaClassName(#typename)⟩†⟨SortParamToTypeParam(#csortvars?, TRUE)⟩ extends Variable
    {
      public Var⟨STRING: ToJavaClassName(#typename)⟩(String name)
      {
        super(name);
      }

      @Override
      protected Var⟨STRING: ToJavaClassName(#typename)⟩Use newVarUse()
      {
        return new Var⟨STRING: ToJavaClassName(#typename)⟩Use(this);
      }

      @Override
		  public Var⟨STRING: ToJavaClassName(#typename)⟩Use use()
		  {
			  return (Var⟨STRING: ToJavaClassName(#typename)⟩Use) super.use();
		  }

      @Override
      public Variable make(Context ctx, String hint)
  	  {
  		  return new Var⟨STRING: ToJavaClassName(#typename)⟩(ctx.makeGlobalName(hint));
  	  }

    }

    ⟧

//--- Default empty constructor

func MaybeSortsToDefaultConstructor(List<Core_csorts_sort>, Text4_text_sort) -> Text4_text_sort
  rule MaybeSortsToDefaultConstructor(csorts?⟦⟧, #classname)
  → text⟦⟧

  rule MaybeSortsToDefaultConstructor(csorts?⟦ ( ##csort*) ⟧, #classname)
  → text⟦private †⟨#classname⟩() {}⟧

//--- Class fields

func MaybeSortsToClassFields(List<Core_csorts_sort>) -> Text4_text_sort
  rule MaybeSortsToClassFields(csorts?⟦⟧)
  → text⟦⟧

  rule MaybeSortsToClassFields(csorts?⟦ ( ##csort* ) ⟧)
  → text⟦
    /* Form arguments */
    †⟨SortsToClassFields(#csort*, 1)⟩⟧

func SortsToClassFields(List<Core_csort_sort>, Numeric) -> Text4_text_sort
  rule SortsToClassFields(csort*⟦⟧, #index)
  → text⟦⟧

  rule SortsToClassFields(csort*⟦ ##csort ##csort* ⟧, #index)
  → text⟦†⟨SortToClassFields(SortToTypes(#csort), #index, 1)⟩†⟨SortsToClassFields(#csort*, Plus(#index, 1))⟩⟧

func SortToClassFields(List<Text4_text_sort>, eager Numeric, eager Numeric) -> Text4_text_sort
  rule SortToClassFields(Cons(#type:Text4_text_sort, List<Text4_text_sort>::Nil), #index, #subindex)
  → text⟦†⟨#type⟩ field†⟨FieldSuffix(#index)⟩;⟧

  rule SortToClassFields(Cons(#type:Text4_text_sort, #types:List<Text4_text_sort>), #index, #subindex)
  → text⟦
     †⟨#type⟩ var†⟨NumberToText(#index)⟩_†⟨NumberToText(#subindex)⟩;
     †⟨SortToClassFields(#types, #index, Plus(#subindex, 1))⟩⟧

//--- Class fields initialization

func MaybeSortsToInitFields(List<Core_csorts_sort>) -> Text4_text_sort
  rule MaybeSortsToInitFields(csorts?⟦⟧)
  → text⟦⟧

  rule MaybeSortsToInitFields(csorts?⟦ ( ##csort* ) ⟧)
  → SortsToInitFields(#csort*, 1)

func SortsToInitFields(List<Core_csort_sort>, Numeric) -> Text4_text_sort
  rule SortsToInitFields(csort*⟦⟧, #index)
  → text⟦⟧

  rule SortsToInitFields(csort*⟦ ##csort ##csort* ⟧, #index)
  → text⟦†⟨SortToInitFields(SortToTypes(#csort), #index, 1)⟩†⟨SortsToInitFields(#csort*, Plus(#index, 1))⟩⟧

func SortToInitFields(List<Text4_text_sort>, eager Numeric, eager Numeric) -> Text4_text_sort
  rule SortToInitFields(Cons(#type:Text4_text_sort, List<Text4_text_sort>::Nil), #index, #subindex)
  → text⟦this.field†⟨FieldSuffix(#index)⟩ = param†⟨FieldSuffix(#index)⟩;⟧

  rule SortToInitFields(Cons(#type:Text4_text_sort, #types:List<Text4_text_sort>), #index, #subindex)
  → text⟦this.var†⟨NumberToText(#index)⟩_†⟨NumberToText(#subindex)⟩ = param†⟨NumberToText(#index)⟩_†⟨NumberToText(#subindex)⟩; †⟨SortToInitFields(#types, #index, Plus(#subindex, 1))⟩⟧

// --- sub function

func MaybeSortsToGetSubs(List<Core_csorts_sort>) -> Text4_text_sort
  rule MaybeSortsToGetSubs(csorts?⟦⟧)
  → text⟦⟧

  rule MaybeSortsToGetSubs(csorts?⟦ ( ##csort* ) ⟧)
  → text⟦
      @Override
      public Term sub(int i) {
       switch (i) {
         †⟨SortsToGetSubs(#csort*, 0)⟩
         default: return null;
        }
      }⟧

func SortsToGetSubs(List<Core_csort_sort>, Numeric) -> Text4_text_sort
  rule SortsToGetSubs(csort*⟦⟧, #index)
  → text⟦⟧

  rule SortsToGetSubs(csort*⟦ ##csort ##csort* ⟧, #index)
  → text⟦†⟨SortToGetSubs(SortToTypes(#csort), #index, 0)⟩†⟨SortsToGetSubs(#csort*, Plus(#index, 1))⟩⟧

func SortToGetSubs(List<Text4_text_sort>, eager Numeric, eager Numeric) -> Text4_text_sort

  rule SortToGetSubs(Cons(#type:Text4_text_sort, List<Text4_text_sort>::Nil), #index, #subindex)
  → text⟦case †⟨NumberToText(#index)⟩: return field†⟨FieldSuffix(Plus(#index, 1))⟩;⟧

  rule SortToGetSubs(Cons(#type:Text4_text_sort, #types:List<Text4_text_sort>), #index, #subindex)
  → SortToGetSubs(#types, #index, Plus(#subindex, 1))

// --- SetSub function

func MaybeSortsToSetSubs(List<Core_csorts_sort>) -> Text4_text_sort
  rule MaybeSortsToSetSubs(csorts?⟦⟧)
  → text⟦⟧

  rule MaybeSortsToSetSubs(csorts?⟦ ( ##csort* ) ⟧)
  → text⟦
      @Override
      public void setSub(int i, Term sub) {
        switch (i) {
             †⟨SortsToSetSubs(#csort*, 0)⟩
             default: throw new IndexOutOfBoundsException();
           }
         }⟧

func SortsToSetSubs(List<Core_csort_sort>, Numeric) -> Text4_text_sort
  rule SortsToSetSubs(csort*⟦⟧, #index)
  → text⟦⟧

  rule SortsToSetSubs(csort*⟦ ##csort ##csort* ⟧, #index)
  → text⟦†⟨SortToSetSubs(SortToTypes(#csort), #index, 0)⟩†⟨SortsToSetSubs(#csort*, Plus(#index, 1))⟩⟧

func SortToSetSubs(List<Text4_text_sort>, eager Numeric, eager Numeric) -> Text4_text_sort

  rule SortToSetSubs(Cons(#type:Text4_text_sort, List<Text4_text_sort>::Nil), #index, #subindex)
  → text⟦
      case †⟨NumberToText(#index)⟩:
        field†⟨FieldSuffix(Plus(#index, 1))⟩ = (†⟨#type⟩) sub;
        break;⟧

  rule SortToSetSubs(Cons(#type:Text4_text_sort, #types:List<Text4_text_sort>), #index, #subindex)
  → SortToSetSubs(#types, #index, Plus(#subindex, 1))

// --- binder function

func MaybeSortsToGetBinder(List<Core_csorts_sort>) -> Text4_text_sort
  rule MaybeSortsToGetBinder(csorts?⟦⟧)
  → text⟦⟧

  rule MaybeSortsToGetBinder(csorts?⟦ ( ##csort* ) ⟧)
  → If<Text4_text_sort>(HasBinders(#csort*),
      ()->text⟦
        @Override
        public Variable binder(int i, int j) {
          switch (i) {
            †⟨SortsToGetBinder(#csort*, 0)⟩
            default: return null;
          }
        }⟧,
      ()->text⟦⟧)

func SortsToGetBinder(List<Core_csort_sort>, Numeric) -> Text4_text_sort
  rule SortsToGetBinder(csort*⟦⟧, #index)
  → text⟦⟧

  rule SortsToGetBinder(csort*⟦ ##csort ##csort* ⟧, #index)
  → text⟦
      †⟨If<Text4_text_sort>(HasBindersSort(#csort),
        ()->text⟦
          case †⟨NumberToText(#index)⟩: {
            switch (j) {
              †⟨SortToGetBinder(SortToTypes(#csort), #index, 0)⟩
              default: return null;
            }
          }⟧,
        ()->text⟦⟧)⟩
      †⟨SortsToGetBinder(#csort*, Plus(#index, 1))⟩⟧

func SortToGetBinder(List<Text4_text_sort>, eager Numeric, eager Numeric) -> Text4_text_sort

  rule SortToGetBinder(Cons(#type:Text4_text_sort, List<Text4_text_sort>::Nil), #index, #subindex)
  → text⟦⟧

  rule SortToGetBinder(Cons(#type:Text4_text_sort, #types:List<Text4_text_sort>), #index, #subindex)
  → text⟦
      case †⟨NumberToText(#subindex)⟩: return var†⟨NumberToText(Plus(#index, 1))⟩_†⟨NumberToText(Plus(#subindex, 1))⟩;
      †⟨SortToGetBinder(#types, #index, Plus(#subindex, 1))⟩⟧

// --- setBinder function

func MaybeSortsToSetBinder(List<Core_csorts_sort>) -> Text4_text_sort
  rule MaybeSortsToSetBinder(csorts?⟦⟧)
  → text⟦⟧

  rule MaybeSortsToSetBinder(csorts?⟦ ( ##csort* ) ⟧)
  → If<Text4_text_sort>(HasBinders(#csort*),
      ()->text⟦
        @Override
        public void setBinder(int i, int j, Variable var) {
         switch (i) {
           †⟨SortsToSetBinder(#csort*, 0)⟩
           default: throw new IndexOutOfBoundsException();
          }
        }⟧,
      ()->text⟦⟧)

func SortsToSetBinder(List<Core_csort_sort>, Numeric) -> Text4_text_sort
  rule SortsToSetBinder(csort*⟦⟧, #index)
  → text⟦⟧

  rule SortsToSetBinder(csort*⟦ ##csort ##csort* ⟧, #index)
  → text⟦
      †⟨If<Text4_text_sort>(HasBindersSort(#csort),
        () -> text⟦
          case †⟨NumberToText(#index)⟩: {
            switch (j) {
              †⟨SortToSetBinder(SortToTypes(#csort), #index, 0)⟩
              default: throw new IndexOutOfBoundsException();
            }
            break;
          }⟧,
        () -> text⟦⟧)⟩
      †⟨SortsToSetBinder(#csort*, Plus(#index, 1))⟩⟧

func SortToSetBinder(List<Text4_text_sort>, eager Numeric, eager Numeric) -> Text4_text_sort

  rule SortToSetBinder(Cons(#type:Text4_text_sort, List<Text4_text_sort>::Nil), #index, #subindex)
  → text⟦⟧

  rule SortToSetBinder(Cons(#type:Text4_text_sort, #types:List<Text4_text_sort>), #index, #subindex)
  → text⟦
      case †⟨NumberToText(#subindex)⟩:
        var†⟨NumberToText(Plus(#index, 1))⟩_†⟨NumberToText(Plus(#subindex, 1))⟩ = (†⟨#type⟩) var;
        break;
      †⟨SortToSetBinder(#types, #index, Plus(#subindex, 1))⟩⟧


//--- Sort to formal parameters

func MaybeSortsToFormalParams(List<Core_csorts_sort>, Text4_text_sort) -> Text4_text_sort
  rule MaybeSortsToFormalParams(csorts?⟦⟧, #sep)
  → text⟦⟧

  rule MaybeSortsToFormalParams(csorts?⟦ ( ##csort*) ⟧, #sep)
  → SortsToFormalParams(#csort*, 1, #sep)

func SortsToFormalParams(List<Core_csort_sort>, Numeric, Text4_text_sort) -> Text4_text_sort
  rule SortsToFormalParams(csort*⟦⟧, #index, #sep)
  → text⟦⟧

  rule SortsToFormalParams(csort*⟦ ##csort ##csort* ⟧, #index, #sep)
  → text⟦†⟨#sep⟩†⟨SortToFormalParams(SortToTypes(#csort), #index, 1)⟩†⟨SortsToFormalParams(#csort*, Plus(#index, 1), text⟦, ⟧)⟩⟧

func SortToFormalParams(List<Text4_text_sort>, Numeric, Numeric) -> Text4_text_sort
  rule SortToFormalParams(Cons(#type:Text4_text_sort, List<Text4_text_sort>::Nil), #index, #subindex)
  → text⟦†⟨#type⟩ param†⟨FieldSuffix(#index)⟩⟧

  rule SortToFormalParams(Cons(#type:Text4_text_sort, #types:List<Text4_text_sort>), #index, #subindex)
  → text⟦†⟨#type⟩ param†⟨NumberToText(#index)⟩_†⟨NumberToText(#subindex)⟩, †⟨SortToFormalParams(#types, #index, Plus(#subindex, 1))⟩⟧

//--- Sort to function call arguments

func MaybeSortsToArgs(List<Core_csorts_sort>) -> Text4_text_sort
  rule MaybeSortsToArgs(csorts?⟦⟧)
  → text⟦⟧

  rule MaybeSortsToArgs(csorts?⟦ ( ##csort*) ⟧)
  → SortsToArgs(#csort*, 1, text⟦⟧)

func SortsToArgs(List<Core_csort_sort>, Numeric, Text4_text_sort) -> Text4_text_sort
  rule SortsToArgs(csort*⟦⟧, #index, #sep)
  → text⟦⟧

  rule SortsToArgs(csort*⟦ ##csort ##csort* ⟧, #index, #sep)
  → text⟦†⟨#sep⟩†⟨SortToArgs(SortToTypes(#csort), #index, 1)⟩†⟨SortsToArgs(#csort*, Plus(#index, 1), text⟦, ⟧)⟩⟧

func SortToArgs(List<Text4_text_sort>, Numeric, Numeric) -> Text4_text_sort
  rule SortToArgs(Cons(#type:Text4_text_sort, List<Text4_text_sort>::Nil), #index, #subindex)
  → text⟦param†⟨FieldSuffix(#index)⟩⟧

  rule SortToArgs(Cons(#type:Text4_text_sort, #types:List<Text4_text_sort>), #index, #subindex)
  → text⟦param†⟨NumberToText(#index)⟩_†⟨NumberToText(#subindex)⟩, †⟨SortToArgs(#types, #index, Plus(#subindex, 1))⟩⟧

//--- Get value method implementation

func MaybeSortsToGetValue(List<Core_csorts_sort>) -> Text4_text_sort
  rule MaybeSortsToGetValue(csorts?⟦⟧)
  → text⟦⟧

  rule MaybeSortsToGetValue(csorts?⟦ ( ##csort* ) ⟧)
  → SortsToGetValue(#csort*, 1)

func SortsToGetValue(List<Core_csort_sort>, eager Numeric) -> Text4_text_sort
  rule SortsToGetValue(csort*⟦ ⟧, #index)
  → text⟦⟧

  rule SortsToGetValue(csort*⟦ ##csort ##csort* ⟧, #index)
  → text⟦†⟨SortToGetValue(SortToTypes(#csort), #index, 1)⟩†⟨SortsToGetValue(#csort*, Plus(#index, 1))⟩⟧

func SortToGetValue(List<Text4_text_sort>, eager Numeric, eager Numeric) -> Text4_text_sort
  rule SortToGetValue(Cons(#type:Text4_text_sort, List<Text4_text_sort>::Nil), #index, #subindex)
  → text⟦public †⟨#type⟩ getField†⟨FieldSuffix(#index)⟩(Context ctx, boolean force) {
            if (force)
               field†⟨FieldSuffix(#index)⟩ = force(ctx, field†⟨FieldSuffix(#index)⟩);
            return field†⟨FieldSuffix(#index)⟩;
         }⟧

  rule SortToGetValue(Cons(#type:Text4_text_sort, #types:List<Text4_text_sort>), #index, #subindex)
  → text⟦public †⟨#type⟩ getVar†⟨NumberToText(#index)⟩_†⟨NumberToText(#subindex)⟩() {
           return var†⟨NumberToText(#index)⟩_†⟨NumberToText(#subindex)⟩;
        }
        †⟨SortToGetValue(#types, #index, Plus(#subindex, 1))⟩⟧

//--- Helpers

func FieldSuffix(Numeric) -> Text4_text_sort
  rule FieldSuffix(#index)
  → NumberToText(#index)

func MaybeSortsCount(List<Core_csorts_sort>) -> Numeric
  rule MaybeSortsCount(csorts?⟦⟧)             → 0
  rule MaybeSortsCount(csorts?⟦ (##csort*) ⟧) → ListLength<Core_csort_sort>(#csort*)

/** whether list of sorts has at least one binders */
func HasBinders(List<Core_csort_sort>) -> Bool
  rule HasBinders(csort*⟦⟧) → FALSE
  rule HasBinders(csort*⟦ ##csort ##csort* ⟧)
  → Or(HasBindersSort(#csort), HasBinders(#csort*))

func HasBindersSort(Core_csort_sort) -> Bool
  rule HasBindersSort(csort⟦ ##CONSTRUCTOR ##csorts? ⟧) → FALSE
  rule HasBindersSort(csort⟦ ##VARIABLE ⟧)              → FALSE
  rule HasBindersSort(csort⟦ [ ##csort ] ##csort2 ⟧)    → TRUE
  rule HasBindersSort(csort⟦ ( ##csort ) ##csort2 ⟧)    → HasBindersSort(#csort2)
  rule HasBindersSort(csort⟦ { ##cmapsort } ⟧)          → FALSE
  rule HasBindersSort(csort⟦ data ##csort ⟧)            → HasBindersSort(#csort)
  rule HasBindersSort(csort⟦ thunk ##csort ⟧)           → HasBindersSort(#csort)

/** Generate classname corresponding to an enumeration value */
func EValueClassName(String) -> Text4_text_sort
rule EValueClassName(#name) → text⟦_⟨STRING:#name⟩⟧

//--- Rename builtin types to avoid conflict with Java types

func FixupPrimitiveType(String) -> String
rule FixupPrimitiveType(#typename)
→ If<String>(StringEqual(#typename, "String"), ()->"StringTerm",
  ()->If<String>(StringEqual(#typename, "Numeric"), ()->"DoubleTerm",
       ()->#typename))


//---- Sort tranlation

func SortParamToTypeParam(List<Core_csortvars_sort>, Bool /* parameterize or parameterization? */) -> Text4_text_sort
  rule SortParamToTypeParam(csortvars?⟦ ⟧, #parameterize)
  → text⟦⟧

  rule SortParamToTypeParam(csortvars?⟦ ∀ ##variable_TOK+ . ⟧, #parameterize)
  → text⟦<†⟨TextFold(Map<String Text4_text_sort>((x) -> text⟦⟨STRING: ToJavaTypeParameter(x)⟩†⟨If<Text4_text_sort>(#parameterize, ()->text⟦ extends Term⟧, ()->text⟦⟧)⟩⟧, #variable_TOK+), text⟦, ⟧)⟩>⟧

func MaybeSortsToTypeParams(List<Core_csorts_sort>) -> Text4_text_sort
  rule MaybeSortsToTypeParams(csorts?⟦ ⟧)
  → text⟦⟧

  rule MaybeSortsToTypeParams(csorts?⟦ ( ##csort* )⟧)
  → text⟦<†⟨TextFold( Flatten<Text4_text_sort>(Map<Core_csort_sort List>((x) -> SortToTypes(x), #csort*)), text⟦, ⟧)⟩>⟧

/** Convert sort annotation to equivalent Java type */
func MaybeSortAnnoToType(List<Core_csortanno_sort>) -> Text4_text_sort
  rule MaybeSortAnnoToType(csortanno?⟦ ⟧)
  → text⟦⟧

  rule MaybeSortAnnoToType(csortanno?⟦ : ##csort ⟧)
  → SortToType(#csort)

/* Translate Transscript sort to equivalent list of Java types:
 * a list of types corresponding to syntactic variable sorts and a type for the last sort
 */
func SortToTypes(Core_csort_sort) -> List<Text4_text_sort>
rule SortToTypes(#csort) → SortToTypes2(#csort, ())

func SortToTypes2(Core_csort_sort, List<Text4_text_sort> /* formal params */) -> List<Text4_text_sort>

  rule SortToTypes2(csort⟦ ##CONSTRUCTOR ##csorts? ⟧, ())
  → Cons(text⟦⟨STRING: ToJavaClassName(FixupPrimitiveType(#CONSTRUCTOR))⟩†⟨MaybeSortsToTypeParams(#csorts?)⟩⟧, Nil)

  rule SortToTypes2(csort⟦ ##CONSTRUCTOR ##csorts? ⟧, Cons(#formal:Text4_text_sort, #formals:List<Text4_text_sort>))
  → Cons(text⟦Closure†⟨NumberToText(Plus(ListLength<Text4_text_sort>(#formals), 1))⟩<⟨STRING:
       ToJavaClassName(FixupPrimitiveType(#CONSTRUCTOR))⟩†⟨MaybeSortsToTypeParams(#csorts?) /* Return type */⟩, †⟨
       TextFold(Cons(#formal, #formals), text⟦, ⟧) /* Formal params */⟩>⟧, Nil)

  rule SortToTypes2(csort⟦ ##VARIABLE ⟧, ())
  → Cons(text⟦⟨STRING: ToJavaTypeParameter(#VARIABLE)⟩⟧, Nil)

  rule SortToTypes2(csort⟦ ##VARIABLE ⟧, Cons(#formal:Text4_text_sort, #formals:List<Text4_text_sort>))
  → Cons(text⟦Closure†⟨NumberToText(Plus(ListLength<Text4_text_sort>(#formals), 1))⟩<⟨STRING: #VARIABLE /* Return type */⟩, †⟨
       TextFold(Cons(#formal, #formals), text⟦, ⟧) /* Formal params */⟩>⟧, Nil)

  rule SortToTypes2(csort⟦ [ ##csort ] ##csort2 ⟧, #formals)
  → Cons(text⟦Var†⟨SortToType(#csort)⟩⟧, SortToTypes2(#csort2, #formals))

  rule SortToTypes2(csort⟦ ( ##csort ) ##csort2 ⟧, #formals)
  → SortToTypes2(#csort2, Concat<Text4_text_sort>(#formals, SortToTypes(#csort)))

  rule SortToTypes2(csort⟦ { ##csort1 : ##csort2 } ⟧, ())
  → Cons(text⟦MapTerm<†⟨SortToType(#csort1)⟩,†⟨SortToType(#csort2)⟩>⟧, Nil)

  rule SortToTypes2(csort⟦ { ##cmapsort } ⟧, Cons(#formal:Text4_text_sort, #formals:List<Text4_text_sort>))
  → Cons(text⟦Closure†⟨NumberToText(Plus(ListLength<Text4_text_sort>(#formals), 1))⟩<MapTerm, †⟨
       TextFold(Cons(#formal, #formals), text⟦, ⟧) /* Formal params */⟩>⟧, Nil)

  rule SortToTypes2(csort⟦ data ##csort ⟧, #formals)
  → SortToTypes2(#csort, #formals)

  rule SortToTypes2(csort⟦ thunk ##csort ⟧, #formals)
  → Cons(text⟦LazyTerm<†⟨SortToType(#csort)⟩>⟧, Nil)

/* Same as above, expect ignore syntactic variables */
func SortToType(Core_csort_sort) -> Text4_text_sort
rule SortToType(#csort) → Last<Text4_text_sort>(SortToTypes(#csort)) // TODO: could optimize

/* Same as above, expect maybe get a sort */
func MaybeSortToType(Option<Core_csort_sort>) -> Text4_text_sort
rule MaybeSortToType(NONE)         → text⟦⟧
rule MaybeSortToType(SOME(#csort:Core_csort_sort)) → Last<Text4_text_sort>(SortToTypes(#csort)) // TODO: could optimize

/* Sort qualifier to type */
func SortQualifierToType(Core_csortqualifier_sort) -> Text4_text_sort
rule SortQualifierToType(csortqualifier⟦ ##csort ::⟧)
→ SortToType(#csort)

//-------------- Translate functions and rules

/* Generate method for function. Start with method signature */
func JavaMethod(String, Text4_text_sort, {String : List<Core_cdecl_sort>},
           { String : Core_cdecl_sort }, { String : Core_cdecl_sort }) -> Text4_text_sort
rule JavaMethod(#constructor, #classname, #rules, #datasorts, #funcsorts)
→ text⟦final public static †⟨
          JavaMethodSig(SetDataSorts(SetFuncSorts(SetClassname(NewEnv, #classname), #funcsorts), #datasorts),
                        UnSOME<Core_cdecl_sort>(MapGet<String Core_cdecl_sort>(#funcsorts, #constructor)),
                        MapGet<String List>(#rules, #constructor))⟩⟧

func JavaMethodSig(eager {String : JavaEnvEntry}, Core_cdecl_sort, Option<List<Core_cdecl_sort>>) -> Text4_text_sort
rule JavaMethodSig(#env, cdecl⟦ ##canno* ##extern_TOK? func ##csortvars? ##csort ##CONSTRUCTOR ##csorts? ⟧, #rules)
→ text⟦†⟨ReturnType(#csortvars?, #csort)⟩⟨STRING:ToJavaMethodName(#CONSTRUCTOR)⟩(Context context†⟨ThunkMakerSig(#csortvars?)⟩†⟨
      JavaSigMaybeArgs(
         SetMethodName(
           SetThunkMaker(
             AddThunk(
               SetContext(SetFnAnno(#env, #canno*), text⟦context⟧, 1),
               ThunkMakerArg(#csortvars?)),
             ThunkMaker(#csort)),
           text⟦⟨STRING: ToJavaMethodName(#CONSTRUCTOR)⟩⟧), #csorts?, #rules)⟩⟧   // )

// --- Generate return type

func ReturnType(List<Core_csortvars_sort>, Core_csort_sort) -> Text4_text_sort
rule ReturnType(#csortvars?, #csort)
→ text⟦†⟨SortParamToTypeParam(#csortvars?, TRUE)⟩ †⟨TextFold(SortToTypes(#csort), text⟦⟧)⟩ ⟧

/* Since Java erases sort parameters at runtime, need to pass them along */
func ThunkMakerSig(List<Core_csortvars_sort>) -> Text4_text_sort
rule ThunkMakerSig(csortvars?⟦             ⟧) → text⟦⟧
rule ThunkMakerSig(csortvars?⟦ ∀ ##variable_TOK+ . ⟧) → TextFold(Map<String Text4_text_sort>((x)->ThunkMakerSigHelper(x), #variable_TOK+), text⟦⟧)

func ThunkMakerSigHelper(String) -> Text4_text_sort
rule ThunkMakerSigHelper(#) → text⟦, ThunkMaker<⟨STRING: #⟩> tm⟨STRING: #⟩⟧

/* Associated function generating thunk maker argument */
func ThunkMakerArg(List<Core_csortvars_sort>) -> Text4_text_sort
rule ThunkMakerArg(csortvars?⟦             ⟧) → text⟦⟧
rule ThunkMakerArg(csortvars?⟦ ∀ ##variable_TOK+ . ⟧) → TextFold(Map<String Text4_text_sort>((x)->ThunkMakerArgHelper(x), #variable_TOK+), text⟦⟧)

func ThunkMakerArgHelper(String) -> Text4_text_sort
rule ThunkMakerArgHelper(#) → text⟦, tm⟨STRING: #⟩⟧

// --- Generate thunk type

func ThunkMaker(Core_csort_sort) -> Text4_text_sort

  rule ThunkMaker(csort⟦ ##CONSTRUCTOR ##csorts? ⟧)
  → text⟦lazy⟨STRING: ToJavaClassName(FixupPrimitiveType(#CONSTRUCTOR))⟩⟧

  rule ThunkMaker(csort⟦ ##VARIABLE ⟧) // polymorphic return type.
  → text⟦tm⟨STRING: #VARIABLE⟩.apply⟧

  rule ThunkMaker(csort⟦ [ ##csort ] ##csort2 ⟧)
  → Error<Text4_text_sort>("Internal error: a function cannot return scoped values")

  rule ThunkMaker(csort⟦ ( ##csort ) ##csort2 ⟧)
  → Error<Text4_text_sort>("Internal error: a function cannot return another function")

  // Java infers map types.
  rule ThunkMaker(csort⟦ { ##cmapsort } ⟧)
  → text⟦lazyMapTerm⟧

  rule ThunkMaker(csort⟦ data ##csort ⟧)
  → ThunkMaker(#csort)

  rule ThunkMaker(csort⟦ thunk ##csort ⟧)
  → ThunkMaker(#csort)

// --- Generate method signature

func JavaSigMaybeArgs(eager {String : JavaEnvEntry}, List<Core_csorts_sort>, Option<List<Core_cdecl_sort>>) -> Text4_text_sort
  rule JavaSigMaybeArgs(#env, csorts?⟦⟧, #rules)
  → JavaBody(#env, #rules)

  rule JavaSigMaybeArgs(#env, csorts?⟦ ( ##csort* ) ⟧, #rules)
  → JavaSigArgs(#env, #csort*, #rules)

// Generate method signature and record the argument names in the environment, keeping them in order
func JavaSigArgs(eager {String : JavaEnvEntry}, List<Core_csort_sort>, Option<List<Core_cdecl_sort>>) -> Text4_text_sort

  rule JavaSigArgs(#env, csort*⟦ ⟧, #rules)
  → JavaBody(#env, #rules)

  rule JavaSigArgs(#env, csort*⟦ ##csort ##csort* ⟧, #rules)
  → JavaSigArg(#env, #csort, FALSE, SortToTypes(#csort), #csort*, #rules)

func JavaSigArg(eager {String : JavaEnvEntry}, Core_csort_sort, Bool /* data? */,
                List<Text4_text_sort> /* formal parameters */, List<Core_csort_sort>, Option<List<Core_cdecl_sort>>) -> Text4_text_sort

 // Monomorphic sort
   rule JavaSigArg(#env, csort⟦ ##CONSTRUCTOR ##csorts? ⟧, #data, Cons(#type:Text4_text_sort, List<Text4_text_sort>::Nil), #csort*, #rules)
   → text⟦, †⟨#type⟩ ⟨STRING: value:String⟩†⟨
       JavaSigArgs(AddThunk(AddParam(AddMaybeData(#env, #data, text⟦⟨STRING: value⟩⟧), text⟦⟨STRING: value⟩⟧, Core_csort_A1(#CONSTRUCTOR, #csorts?)), text⟦⟨STRING: value⟩⟧), #csort*, #rules)⟩⟧

   // Sort variable
   rule JavaSigArg(#env, csort⟦ ##VARIABLE ⟧, #data, Cons(#type:Text4_text_sort, List<Text4_text_sort>::Nil), #csort*, #rules)
   → text⟦, †⟨#type⟩ ⟨STRING: value:String⟩†⟨
       JavaSigArgs(AddThunk(AddParam(AddMaybeData(#env, #data, text⟦⟨STRING: value⟩⟧), text⟦⟨STRING: value⟩⟧, Core_csort_A2(#VARIABLE)), text⟦⟨STRING: value⟩⟧), #csort*, #rules)⟩⟧

   // Syntactic variable sort
   //rule JavaSigArgs(#env, csort⟦ [ ##csort ] ##csort2 ⟧, #data, Cons(#type, #types), #rules)
   rule JavaSigArg(#env, Core_csort_A3(#csort:Core_csort_sort, #csort2:Core_csort_sort), #data, Cons(#type:Text4_text_sort, #types:List<Text4_text_sort>), #csort*, #rules)
   → text⟦, †⟨#type⟩ ⟨STRING: var:String⟩†⟨
       JavaSigArg(AddThunk(AddParam(#env, text⟦⟨STRING: var⟩⟧, #csort), text⟦⟨STRING: var⟩⟧), #csort2, #data, #types, #csort*, #rules)⟩⟧

   // Formal parameter sort
   //rule JavaSigArgs(#env, csort⟦ ( ##csort ) ##csort2 ⟧, #data, #rules)
   rule JavaSigArg(#env, Core_csort_A4(#csort:Core_csort_sort, #csort2:Core_csort_sort), #data, #types, #csort*, #rules)
   → JavaSigArg(#env, #csort2, #data, #types, #csort*, #rules)

   rule JavaSigArg(#env, csort⟦ { ##cmapsort* } ⟧, #data, Cons(#type:Text4_text_sort, List<Text4_text_sort>::Nil), #csort*, #rules)
   → text⟦, †⟨#type⟩ ⟨STRING: map:String⟩†⟨
       JavaSigArgs(AddThunk(AddParam(AddMaybeData(#env, #data, text⟦⟨STRING: map⟩⟧), text⟦⟨STRING: map⟩⟧, Core_csort_A5(#cmapsort*)), text⟦⟨STRING: map⟩⟧), #csort*, #rules)⟩⟧

   rule JavaSigArg(#env, csort⟦ data ##csort ⟧, #data, #types, #csort*, #rules)
   → JavaSigArg(#env, #csort, TRUE, #types, #csort*, #rules)

   rule JavaSigArg(#env, csort⟦ thunk ##csort ⟧, #data, #types, #csort*, #rules)
   → JavaSigArg(#env, #csort, #data, #types, #csort*, #rules)

// --- Generate method body

func JavaBody(eager {String : JavaEnvEntry}, Option<List<Core_cdecl_sort>>) -> Text4_text_sort

  // No rules: it must be an external function (not yet checked but should)
  rule JavaBody(#env, NONE)
  → text⟦) {
        return †⟨GetClassname(#env)⟩Extern.†⟨GetMethodName(#env)⟩(context†⟨TextFold(GetThunk(#env), text⟦, ⟧)⟩);
      }⟧

  // At least one rule: not external
  rule JavaBody(#env, SOME(#rules:List<Core_cdecl_sort>))
  → text⟦
    ) {
        †⟨TextFold(Map<Core_cdecl_sort Text4_text_sort>((x) -> JavaBodyRule(#env, x), #rules), text⟦⟧)⟩

        †⟨FallbackThunk(#env, HasAnnotation("Fallback", GetFnAnno(#env)))⟩
      }⟧

// Generate code to normalize arguments marked as 'data'
func JavaApplyData(Text4_text_sort) -> Text4_text_sort
rule JavaApplyData(#term)
→ text⟦†⟨#term⟩.eval(context).release();⟧

/* Generate fallback thunk (or error) if function is not complete */
func FallbackThunk({String : JavaEnvEntry}, Bool) -> Text4_text_sort

  rule FallbackThunk(#env, TRUE)  → text⟦⟧

  rule FallbackThunk(#env, FALSE)
  //→ text⟦return †⟨GetThunkMaker(#env)⟩(c -> †⟨GetMethodName(#env)⟩(c†⟨TextFold(GetThunk(#env), text⟦, ⟧)⟩));⟧
  → text⟦throw new RuntimeException("Missing case");⟧

// --- Start method body

func JavaBodyRule(eager {String : JavaEnvEntry}, Core_cdecl_sort) -> Text4_text_sort
  rule JavaBodyRule(#env, cdecl⟦ rule ##CONSTRUCTOR → ##cterm ⟧)
  → JavaContractum(#env, #cterm)

  rule JavaBodyRule(#env, cdecl⟦ rule ##CONSTRUCTOR ( ##cterm* ) → ##cterm2 ⟧)
  → JavaPattern(#env, #cterm*, #cterm2)

// --- Pattern matching

func JavaPattern(eager {String : JavaEnvEntry}, List<Core_cterm_sort>, Core_cterm_sort) -> Text4_text_sort

// TODO: case where there is only one rule.
rule JavaPattern(#env, #cterms, #contractum)
→ text⟦⟨STRING: label:String⟩:{†⟨JavaCasePattern(SetLabel(#env, text⟦⟨STRING: label⟩⟧), #cterms, #contractum)⟩
}⟧

// Data sort helper indicating whether the current term is a method parameter, or an argument (local variable).
enum Storage | PARAMETER | ARGUMENT

// Iterate over top-level pattern arguments
func JavaCasePattern(eager {String : JavaEnvEntry}, List<Core_cterm_sort>, Core_cterm_sort) -> Text4_text_sort

rule JavaCasePattern(#env, cterm*⟦ ⟧, #contractum)
→ JavaContractum(#env, #contractum)

rule JavaCasePattern(#env, cterm*⟦ ##cterm ##cterm* ⟧, #contractum)
→ JavaPatternTerm(NextParam(#env), #cterm, CurrentParam(#env), SOME(CurrentParamSort(#env)), PARAMETER, (env) -> JavaCasePattern(env, #cterm*, #contractum))

// --- Term pattern matching

func JavaPatternTerm(eager {String : JavaEnvEntry}, Core_cterm_sort, Text4_text_sort /* Current term */,
                     Option<Core_csort_sort> /* Sort when known */, Storage, ({String : JavaEnvEntry}) -> Text4_text_sort) -> Text4_text_sort

  rule JavaPatternTerm(#env, cterm⟦ ##csortqualifier* ##CONSTRUCTOR ##csortargs? ##cterms? ##csortanno? ⟧, #term, #csort, #storage, #cont)
  → text⟦
      †⟨ForceParameter(#env, #term, #storage)⟩
      †⟨EValueClassName(#CONSTRUCTOR)⟩†⟨ResolveTypeParamCons(#csort)⟩ ⟨STRING: value:String⟩ = †⟨#term⟩.†⟨MethodNameAs(#CONSTRUCTOR)⟩(context);
      if (⟨STRING: value⟩ == null) {
        break †⟨GetLabel(#env)⟩;
      }†⟨
      JavaPatternMaybeSubs(#env, #cterms?, text⟦⟨STRING: value⟩⟧, FindForm(Maybe<Core_csort_sort Option>((x) -> LookupDataSortDecl(GetDataSorts(#env), x), #csort), #CONSTRUCTOR), #cont)⟩⟧

  // TODO: check literal
  rule JavaPatternTerm(#env, cterm⟦ ##cliteral ⟧, #term, #csort, #storage, #cont)
  → #cont(#env)

  // REVISIT: bound variable reordering, ie. (x)(y)#(y, x)
  // For now only accepts patterns of the form (x)...(y)#(x, ... y)
  rule JavaPatternTerm(#env, cterm⟦ ##METAVAR ##cterms? ##csubst? ##csortanno? ⟧, #term, #csort, #storage, #cont)
  → text⟦†⟨JavaPatternMaybeMetaArgs(#env, #METAVAR, #csubst?, #term, #csort, #cont)⟩⟧

  rule JavaPatternTerm(#env, cterm⟦ ##VARIABLE ⟧, #term, #csort, #storage, #cont)
  → JavaPatternVariable(MaybeGetVar(#env, #VARIABLE), #env, #VARIABLE, #term, #cont)

  rule JavaPatternTerm(#env, cterm⟦ [ v ##csortanno? ] ##cterm ⟧, #term, #csort, PARAMETER, #cont)
  //rule JavaPatternTerm(#env, Core_cterm_A6(#csortanno?:List<Core_csortanno_sort>, [x] -> #cterm[x]: Core_cterm_sort), #term, #csort, PARAMETER, #cont)
  → JavaPatternTerm(NextParam(AddVar(#env, var:String, Bound(#term, MaybeSortAnnoToSort(#csortanno?)))), #cterm[var], CurrentParam(#env), SOME(CurrentParamSort(#env)), PARAMETER, #cont)

  rule JavaPatternTerm(#env, cterm⟦ [ v ##csortanno? ] ##cterm ⟧, #term, #csort, ARGUMENT, #cont)
  //rule JavaPatternTerm(#env, Core_cterm_A6(#csortanno?:List<Core_csortanno_sort>, [x] -> #cterm[x]: Core_cterm_sort), #term,  #csort, ARGUMENT, #cont)
  → text⟦
      Variable ⟨STRING: boundvar:String⟩ = †⟨GetParent(#env)⟩.getVar†⟨NumberToText(GetSubIndex(#env))⟩_†⟨NumberToText(GetSubBinderIndex(#env))⟩();†⟨
      JavaPatternTerm(IncSubBinderIndex(AddVar(#env, var:String, Bound(text⟦⟨STRING: boundvar⟩⟧, MaybeSortAnnoToSort(#csortanno?)))), #cterm[var], #term, #csort, ARGUMENT, #cont)⟩⟧

  //rule JavaPatternTerm(#env, cterm⟦ ( ##VARIABLE ##csortanno? ) ##cterm ⟧, #term, #storage, #cont)
  rule JavaPatternTerm(#env, Core_cterm_A7(#csortanno?:List<Core_csortanno_sort>, [x] -> #cterm[x]: Core_cterm_sort), #term, #csort, #storage, #cont)
  → JavaPatternTerm(#env, #cterm[var:String], #term, #csort, #storage, #cont)

// Pattern matching on construction arguments.
func JavaPatternMaybeSubs(eager {String : JavaEnvEntry}, List<Core_cterms_sort>, Text4_text_sort, Option<Core_cform_sort>, ({String : JavaEnvEntry}) -> Text4_text_sort) -> Text4_text_sort

  rule JavaPatternMaybeSubs(#env, cterms?⟦⟧, #term, #cform, #cont)
  → #cont(#env)

  rule JavaPatternMaybeSubs(#env, cterms?⟦()⟧, #term, #cform, #cont)
  → #cont(#env)

  rule JavaPatternMaybeSubs(#env, cterms?⟦ ( ##cterm* ) ⟧, #term, SOME(cform⟦ ##CONSTRUCTOR ( ##csort* ) ⟧), #cont)
  → JavaPatternSubs(#env, #cterm*, #term, 1, SOME(#csort*), #cont)

  rule JavaPatternMaybeSubs(#env, cterms?⟦ ( ##cterm* ) ⟧, #term, NONE, #cont)
  → JavaPatternSubs(#env, #cterm*, #term, 1, NONE, #cont)

func JavaPatternSubs(eager {String : JavaEnvEntry}, List<Core_cterm_sort>,
                     Text4_text_sort /* parent term */, Numeric /* Current subindex */, Option<List<Core_csort_sort>>, /* sub sorts, if known */
                     ({String : JavaEnvEntry}) -> Text4_text_sort) -> Text4_text_sort

  rule JavaPatternSubs(#env, cterm*⟦⟧,  #term, #index, #csort*, #cont)
  → #cont(#env)

  rule JavaPatternSubs(#env, cterm*⟦ ##cterm ##cterm* ⟧, #term, #index, #csort*, #cont)
  → text⟦
      †⟨MaybeSortToType(SubSort(#env, #cterm, #csort*))⟩ ⟨STRING: sub:String⟩ = †⟨#term⟩.getField†⟨FieldSuffix(#index)⟩(†⟨GetContext(#env)⟩, true);†⟨
      JavaPatternTerm(SetSubBinderIndex(SetParent(SetSubIndex(#env, #index), #term), 1),
                  #cterm, text⟦⟨STRING: sub⟩⟧, SubSort(#env, #cterm, #csort*), ARGUMENT,
                  (x) -> JavaPatternSubs(x, #cterm*, #term, NumberPlus(#index, 1), Maybe<List List>((x)->Tail<Core_csort_sort>(x), #csort*), #cont))⟩⟧


// --- Variable pattern matching outside of meta

func JavaPatternVariable(Option<VarEntry>, eager {String : JavaEnvEntry}, String, Text4_text_sort /* Current term */, ({String : JavaEnvEntry}) -> Text4_text_sort) -> Text4_text_sort

  // First variable occurrence: the term is a variable use.
  rule JavaPatternVariable(NONE, #env, #variable, #term, #cont)
  → text⟦
      Variable ⟨STRING: var:String⟩ = †⟨#term⟩.variable();†⟨#cont(AddVar(#env, #variable, Free(text⟦⟨STRING: var⟩⟧, NONE)))⟩⟧

  // Bound variable
  // TODO: update term's parent with evaluated term.
  // TODO: release acquired references.
  rule JavaPatternVariable(SOME(VarEntry::Bound(#javavar:Text4_text_sort)), #env, #variable, #term, #cont)
  → text⟦
      †⟨#term⟩ = force(context, †⟨#term⟩);
      if (!isVariableUse(†⟨#term⟩) || †⟨#javavar⟩ != †⟨#term⟩.variable()) {
        break †⟨GetLabel(#env)⟩;
      }†⟨#cont(#env)⟩⟧

// --- pattern matching inside meta

// Iterate over meta arguments and find corresponding bound variables, and update environment
func JavaPatternMaybeMetaArgs(eager {String : JavaEnvEntry}, String, List<Core_csubst_sort>, Text4_text_sort /* Current term */, Option<Core_csort_sort>, ({String : JavaEnvEntry}) -> Text4_text_sort) -> Text4_text_sort

  rule JavaPatternMaybeMetaArgs(#env, #metavar, csubst?⟦⟧, #term, #csort, #cont)
  → #cont(AddMeta(#env, #metavar, #term, ()))

  // meta substitution
  rule JavaPatternMaybeMetaArgs(#env, #metavar, csubst?⟦ [ ##cterm* ] ⟧, #term, #csort, #cont)
  → #cont(AddMeta(#env, #metavar, #term, Map<Core_cterm_sort Text4_text_sort>((x) -> JavaFindBoundVar(#env, x), #cterm*)))

func JavaFindBoundVar(eager {String : JavaEnvEntry},Core_cterm_sort) -> Text4_text_sort

  rule JavaFindBoundVar(#env, cterm⟦ ##VARIABLE ⟧)
  → GetVar(#env, #VARIABLE)

// Interpreter does not handle Fallback
//-(Fallback):
//JavaFindBoundVar(#env, #cterm)
//→ $(Error, $(:, "Invalid argument in metavariable: ", $(Show, #cterm)))

//  --------- Helper function for pattern matching

/* Generate force call if term is a parameter */
func ForceParameter({String : JavaEnvEntry}, Text4_text_sort, Storage) -> Text4_text_sort
  rule ForceParameter(#env, #term, PARAMETER) →  text⟦†⟨#term⟩ = force(†⟨GetContext(#env)⟩, †⟨#term⟩);⟧
  rule ForceParameter(#env, #term, #storage)  →  text⟦⟧

/* Extract type parameter from constructor sort */
func ResolveTypeParamCons(Option<Core_csort_sort>) -> Text4_text_sort
  rule ResolveTypeParamCons(NONE)
  → text⟦⟧

  rule ResolveTypeParamCons(SOME(csort⟦ ##CONSTRUCTOR ##csorts? ⟧))
  → MaybeSortsToTypeParams(#csorts?)

  rule ResolveTypeParamCons(SOME(#:Core_csort_sort))
  → text⟦⟧

/* Get sub sort. Get it from term and if not available on sort */
func SubSort(eager {String : JavaEnvEntry}, Core_cterm_sort, Option<List<Core_csort_sort>>) -> Option<Core_csort_sort>
rule SubSort(#env, #cterm, #csorts?)
→ SubSort2(SubSortFromTerm(#env, #cterm), #csorts?)

func SubSort2(Option<Core_csort_sort>, Option<List<Core_csort_sort>>) -> Option<Core_csort_sort>

  rule SubSort2(SOME(#csort:Core_csort_sort), #)
  → SOME(#csort)

  rule SubSort2(NONE, NONE)
  → NONE

  rule SubSort2(NONE, SOME(csort*⟦ ##csort ##csort* ⟧))
  → SOME(#csort)

func SubSortFromTerm(eager {String : JavaEnvEntry}, Core_cterm_sort) -> Option<Core_csort_sort>

  // TODO: support for multiple sort qualifiers
  rule SubSortFromTerm(#env, cterm⟦ ##csort :: ##csortqualifier* ##CONSTRUCTOR ##csortargs?  ##cterms? ##csortanno? ⟧)
  → SOME(#csort)

  rule SubSortFromTerm(#env, cterm⟦ ##CONSTRUCTOR ##csortargs?  ##cterms? ##csortanno? ⟧)
  → NONE

  rule SubSortFromTerm(#env, cterm⟦ ##STRING ⟧)
  → SOME(csort⟦ String ⟧)

  rule SubSortFromTerm(#env, cterm⟦ ##NUMBER ⟧)
  → SOME(csort⟦ Numeric⟧)

  rule SubSortFromTerm(#env, cterm⟦ ##VARIABLE ⟧)
  → SubSortFromVar(MaybeGetVar(#env, #VARIABLE), #VARIABLE)

  rule SubSortFromTerm(#env, cterm⟦ ##METAVAR ##cterms? ##csubst? ##csortanno? ⟧)
  → MaybeSortAnnoToSort(#csortanno?)

  // TODO: maybe error?
  rule SubSortFromTerm(#env, cterm⟦ [ x ##csortanno? ] ##cterm ⟧)
  //rule SubSortFromTerm(#env, Core_cterm_A6(#csortanno?:List<Core_csortanno_sort>, [x] -> #cterm[x] : Core_cterm_sort))
  → SubSortFromTerm(#env, #cterm[var:String])

  // TODO: maybe error?
  rule SubSortFromTerm(#env, cterm⟦ ( f ##csortanno? ) ##cterm ⟧)
  //rule SubSortFromTerm(#env, Core_cterm_A7(#csortanno?:List<Core_csortanno_sort>, [x] -> #cterm[x] : Core_cterm_sort))
  → SubSortFromTerm(#env, #cterm[var:String])

func SubSortFromVar(Option<VarEntry>, String) -> Option<Core_csort_sort>

  rule SubSortFromVar(NONE, #var)
  → NONE

  rule SubSortFromVar(SOME(#varEntry:VarEntry), #var)
  → UnVarSort(#varEntry)

// ----- Contraction

func JavaContractum(eager {String : JavaEnvEntry}, Core_cterm_sort) -> Text4_text_sort
rule JavaContractum(#env, #term)
→ JavaStatementTerm(#env, #term)

func JavaStatementTerm(eager {String : JavaEnvEntry}, eager Core_cterm_sort) -> Text4_text_sort
rule JavaStatementTerm(#env, #cterm)
→ text⟦†⟨JavaFreshesTerm(SetCounter(#env, 0), #cterm, (env) ->
     text⟦†⟨If<Text4_text_sort>(GetTail(env), ()->text⟦return ⟧, ()->text⟦⟧)⟩†⟨JavaExprTerm(SetCounter(env, 0), #cterm, (env) -> text⟦⟧)⟩;⟧)⟩⟧

// ---- Generate fresh variable - Only in Java statement mode

func JavaFreshesTerm(eager {String : JavaEnvEntry}, Core_cterm_sort, ({String : JavaEnvEntry}) -> Text4_text_sort) -> Text4_text_sort

  rule JavaFreshesTerm(#env, cterm⟦ ##csortqualifier* ##CONSTRUCTOR ##csortargs? ##cterms? ##csortanno? ⟧, #cont)
  → JavaFreshesMaybeTerms(#env, #cterms?, #cont)

  rule JavaFreshesTerm(#env, cterm⟦ ##cliteral ⟧, #cont)
  → #cont(#env)

  rule JavaFreshesTerm(#env, cterm⟦ ##VARIABLE ##csortanno? ⟧, #cont)
  → JavaFreshesVariable(#env, MaybeGetVar(#env, #VARIABLE), #VARIABLE, #csortanno?, #cont)

  rule JavaFreshesTerm(#env, cterm⟦ ##METAVAR ##cterms? ##csubst? ##csortanno? ⟧, #cont)
  → JavaFreshesMaybeTerms(#env, #cterms?, (env) -> JavaFreshesMaybeSubst(env, #csubst?, #cont))

//  rule JavaFreshesTerm(#env, Core_cterm_A6(#csortanno?:List<Core_csortanno_sort>, [x] -> #cterm[x]:Core_cterm_sort), #cont)
  rule JavaFreshesTerm(#env, cterm⟦ [ x ##csortanno? ] ##cterm⟧, #cont)
  → JavaFreshesVariable(#env, NONE, var:String, #csortanno?, (env) -> JavaFreshesTerm(env, #cterm[var], #cont))

  //→ text⟦
  //Variable fresh†⟨NumberToText(GetCounter(#env))⟩ = context.makeVariable("x");†⟨JavaFreshesTerm(IncCounter(AddVar(#env, var:Core_cterm_sort, Fresh(text⟦fresh†⟨NumberToText(GetCounter(#env))⟩⟧, MaybeSortAnnoToSort(#csortanno?)))), #cterm[var], #cont)⟩⟧

  // TODO: track var to avoid generating dummy fresh variables
  //rule JavaFreshesTerm(#env, Core_cterm_A7(#csortanno?:List<Core_csortanno_sort>, [x] -> #cterm[x]:Core_cterm_sort), #cont)
  rule JavaFreshesTerm(#env, cterm⟦ ( f ##csortanno? ) ##cterm⟧, #cont)
  → JavaFreshesTerm(#env, #cterm[var:String], #cont)

  rule JavaFreshesTerm(#env, cterm⟦ thunk ##cterm ⟧, #cont)
  → JavaFreshesTerm(#env, #cterm, #cont)

func JavaFreshesMaybeTerms(eager {String : JavaEnvEntry}, List<Core_cterms_sort>, ({String : JavaEnvEntry}) -> Text4_text_sort) -> Text4_text_sort

  rule JavaFreshesMaybeTerms(#env, cterms?⟦⟧, #cont)
  → #cont(#env)

  rule JavaFreshesMaybeTerms(#env, cterms?⟦ () ⟧, #cont)
  → #cont(#env)

  rule JavaFreshesMaybeTerms(#env, cterms?⟦ ( ##cterm* ) ⟧, #cont)
  → JavaFreshesTerms(#env, #cterm*, #cont)

func JavaFreshesTerms(eager {String : JavaEnvEntry}, List<Core_cterm_sort>, ({String : JavaEnvEntry}) -> Text4_text_sort) -> Text4_text_sort
  rule JavaFreshesTerms(#env, cterm*⟦⟧, #cont)
  → #cont(#env)

  rule JavaFreshesTerms(#env, cterm*⟦ ##cterm ##cterm* ⟧, #cont)
  → JavaFreshesTerm(#env, #cterm, (env) -> JavaFreshesTerms(env, #cterm*, #cont))

func JavaFreshesMaybeSubst(eager {String : JavaEnvEntry}, List<Core_csubst_sort>, ({String : JavaEnvEntry}) -> Text4_text_sort) -> Text4_text_sort
  rule JavaFreshesMaybeSubst(#env, csubst?⟦⟧, #cont)
  → #cont(#env)

  rule JavaFreshesMaybeSubst(#env, csubst?⟦ [ ##cterm* ] ⟧, #cont)
  → JavaFreshesTerms(#env, #cterm*, #cont)

func JavaFreshesVariable(eager {String : JavaEnvEntry}, Option<VarEntry>, String, List<Core_csortanno_sort>, ({String : JavaEnvEntry}) -> Text4_text_sort) -> Text4_text_sort

  // untyped fresh var
  rule JavaFreshesVariable(#env, NONE, #var, csortanno?⟦  ⟧, #cont)
  → text⟦
      VarStringTerm fresh†⟨NumberToText(GetCounter(#env))⟩ = varStringTerm(†⟨GetContext(#env)⟩, †⟨Text-QuoteEscape(Show<String>(#var))⟩);†⟨
        #cont(IncCounter(AddVar(#env, #var, Fresh(text⟦fresh†⟨NumberToText(GetCounter(#env))⟩⟧, NONE))))⟩⟧

  // New typed fresh variable
  rule JavaFreshesVariable(#env, NONE, #var, csortanno?⟦ : ##csort ⟧, #cont)
  → text⟦
      Var†⟨SortToType(#csort)⟩ fresh†⟨NumberToText(GetCounter(#env))⟩ = var†⟨SortToType(#csort)⟩(†⟨GetContext(#env)⟩, †⟨Text-QuoteEscape(Show<String>(#var))⟩);⟨
        #cont(IncCounter(AddVar(#env, #var, Fresh(text⟦fresh†⟨NumberToText(GetCounter(#env))⟩⟧, SOME(#csort)))))⟩⟧

  // Variable already exists
  rule JavaFreshesVariable(#env, SOME(#:VarEntry), #var, #csortanno?, #cont)
  → #cont(#env)

// ---- Generate term code - Java expression mode

func JavaExprTerm(eager {String : JavaEnvEntry}, Core_cterm_sort, ({String : JavaEnvEntry}) -> Text4_text_sort) -> Text4_text_sort

  rule JavaExprTerm(#env, cterm⟦ ##csortqualifier* ##CONSTRUCTOR ##csortargs? ##cterms? ##csortanno? ⟧, #cont)
  → JavaExprConstruction(#env, #CONSTRUCTOR, #csortargs?, #cterms?, #csortanno?, #cont)

  rule JavaExprTerm(#env, cterm⟦ ##cliteral ⟧, #cont)
  → text⟦
      †⟨Literal(#cliteral)⟩†⟨#cont(#env)⟩⟧

  rule JavaExprTerm(#env, cterm⟦ ##VARIABLE ##csortanno? ⟧, #cont)
  → JavaExprMaybeVar(#env, MaybeGetVar(#env, #VARIABLE), #VARIABLE, #cont)

  rule JavaExprTerm(#env, cterm⟦ ##METAVAR ##cterms? ##csubst? ##csortanno? ⟧, #cont)
  → JavaExprMeta(#env, #METAVAR, #cterms?, #csubst?, #cont)

  rule JavaExprTerm(#env, cterm⟦ [ x ##csortanno? ] ##cterm ⟧, #cont)
  //rule JavaExprTerm(#env, Core_cterm_A6(#csortanno?:List<Core_csortanno_sort>, [x] -> #cterm[x] : Core_cterm_sort), #cont)
  → text⟦
      fresh†⟨NumberToText(GetCounter(#env))⟩, †⟨JavaExprTerm(AddVar(IncCounter(#env), var:String, Fresh(text⟦fresh†⟨NumberToText(GetCounter(#env))⟩⟧, MaybeSortAnnoToSort(#csortanno?))), #cterm[var], #cont)⟩⟧

  rule JavaExprTerm(#env, cterm⟦ ( f ##csortanno? ) ##cterm ⟧, #cont)
  //rule JavaExprTerm(#env, Core_cterm_A7(#csortanno?:List<Core_csortanno_sort>, [x] -> #cterm[x] : Core_cterm_sort), #cont)
  → text⟦newClosure((†⟨GetContext(NewContext(#env))⟩†⟨JavaExprLambda(NewContext(#env), Core_cterm_A7(#csortanno?, [y:String] -> #cterm[y]), #cont)⟩⟧

  rule JavaExprTerm(#env, cterm⟦ thunk ##cterm ⟧, #cont)
  → text⟦thunk(†⟨GetContext(NewContext(#env))⟩ -> ⟨JavaExprTerm(NewContext(#env), #cterm, (env) -> text⟦)⟨#cont(env)⟩⟧)⟩⟧


// ---- construction expression

func JavaExprConstruction(eager {String : JavaEnvEntry}, String,  List<Core_csortargs_sort>, List<Core_cterms_sort>, List<Core_csortanno_sort>, ({String : JavaEnvEntry}) -> Text4_text_sort) -> Text4_text_sort

  rule JavaExprConstruction(#env, #constructor, #csortargs?, #cterms?, #csortanno?, #cont)
  → text⟦⟨STRING: ToJavaMethodName(#constructor)⟩(context†⟨JavaExprThunkArgs(#csortargs?)⟩†⟨JavaExprMaybeTerms(#env, #cterms?, #cont)⟩⟧

func JavaExprMaybeTerms(eager {String : JavaEnvEntry}, List<Core_cterms_sort>, ({String : JavaEnvEntry}) -> Text4_text_sort) -> Text4_text_sort

  rule JavaExprMaybeTerms(#env, cterms?⟦⟧, #cont)
  → text⟦)†⟨#cont(#env)⟩⟧

  rule JavaExprMaybeTerms(#env, cterms?⟦()⟧, #cont)
  → text⟦)†⟨#cont(#env)⟩⟧

  rule JavaExprMaybeTerms(#env, cterms?⟦ ( ##cterm* ) ⟧, #cont)
  → JavaExprTerms(#env, #cterm*, #cont)

func JavaExprTerms(eager {String : JavaEnvEntry}, List<Core_cterm_sort>, ({String : JavaEnvEntry}) -> Text4_text_sort) -> Text4_text_sort
  rule JavaExprTerms(#env, cterm*⟦⟧, #cont)
  → text⟦)†⟨#cont(#env)⟩⟧

  rule JavaExprTerms(#env, cterm*⟦ ##cterm ##cterm* ⟧, #cont)
  → text⟦, †⟨JavaExprTerm(#env, #cterm, (env) -> JavaExprTerms(env, #cterm*, #cont))⟩⟧


func JavaExprThunkArgs(List<Core_csortargs_sort>) -> Text4_text_sort
  rule JavaExprThunkArgs(csortargs?⟦              ⟧) → text⟦⟧
  rule JavaExprThunkArgs(csortargs?⟦ < ##csort* > ⟧) → TextFold(Map<Core_csort_sort Text4_text_sort>((x)->JavaExprThunkArg(x), #csort*), text⟦⟧)

func JavaExprThunkArg(Core_csort_sort) -> Text4_text_sort
  rule JavaExprThunkArg(csort⟦ ##VARIABLE ⟧) → text⟦, tm⟨STRING: #VARIABLE⟩⟧
  rule JavaExprThunkArg(csort⟦ ##csort    ⟧) → text⟦, †⟨ThunkMaker(#csort)⟩Maker()⟧

// --- Variable

func JavaExprMaybeVar(eager {String : JavaEnvEntry}, Option<VarEntry>, String, ({String : JavaEnvEntry}) -> Text4_text_sort) -> Text4_text_sort

  rule JavaExprMaybeVar(#env, NONE, #svar, #cont) // Fresh variable must have been declared
  → #cont(#env)

  rule JavaExprMaybeVar(#env, SOME(VarEntry::Formal(#var:Text4_text_sort, #type:Option<Core_csort_sort>)), #svar, #cont)
  → text⟦†⟨#var⟩†⟨#cont(#env)⟩⟧

  // fallback
  rule JavaExprMaybeVar(#env, SOME(#varEntry:VarEntry), #svar, #cont)
  → text⟦†⟨UnVarVar(#varEntry)⟩.use()†⟨#cont(#env)⟩⟧

// ----  metavar expression

func JavaExprMeta(eager {String : JavaEnvEntry}, String, List<Core_cterms_sort>, List<Core_csubst_sort>, ({String : JavaEnvEntry}) -> Text4_text_sort) -> Text4_text_sort

   // TODO: check that metavar is defined

  // No arguments -> just return the metavariable.
  rule JavaExprMeta(#env, #metavar, cterms?⟦⟧, csubst?⟦⟧, #cont)
  → text⟦†⟨GetMetaVar(#env, #metavar)⟩†⟨#cont(#env)⟩⟧

  // No arguments in parenthesis -> call lambda with no param.
  rule JavaExprMeta(#env, #metavar, cterms?⟦ () ⟧, csubst?⟦⟧, #cont)
  → text⟦†⟨GetMetaVar(#env, #metavar)⟩.eval(context)†⟨#cont(#env)⟩⟧

  // Apply arguments -> call lambda
  rule JavaExprMeta(#env, #metavar, cterms?⟦ ( ##cterm* ) ⟧, csubst?⟦⟧, #cont)
  → text⟦†⟨GetMetaVar(#env, #metavar)⟩.eval(context†⟨JavaExprTerms(#env, #cterm*, #cont)⟩⟧

  // Substitution
  rule JavaExprMeta(#env, #metavar, cterms?⟦⟧, csubst?⟦ [ ##cterm* ] ⟧, #cont)
  → text⟦subst(context, †⟨GetMetaVar(#env, #metavar)⟩†⟨JavaExprMetaArgs(#env, GetMetaArgs(#env, #metavar), #cterm*, #cont)⟩⟧

  // TODO: call and subsitution

func JavaExprMetaArgs(eager {String : JavaEnvEntry}, List<Text4_text_sort> /* pattern args */, List<Core_cterm_sort> /* contraction args */,
                     ({String : JavaEnvEntry}) -> Text4_text_sort) -> Text4_text_sort

  rule JavaExprMetaArgs(#env, (), #args,  #cont)
  → text⟦)†⟨#cont(#env)⟩⟧

  rule JavaExprMetaArgs(#env, Cons(#binder:Text4_text_sort,  #binders: List<Text4_text_sort>), Cons(#arg:Core_cterm_sort, #args:List<Core_cterm_sort>), #cont)
  → text⟦,†⟨#binder⟩,†⟨JavaExprTerm(#env, #arg, (env) -> JavaExprMetaArgs(env, #binders, #args, #cont))⟩⟧

// ---- lambda expression

func JavaExprLambda(eager {String : JavaEnvEntry}, Core_cterm_sort, ({String : JavaEnvEntry}) -> Text4_text_sort) -> Text4_text_sort

  rule JavaExprLambda(#env, Core_cterm_A7(#csortanno?:List<Core_csortanno_sort>, [x] -> #cterm[x]: Core_cterm_sort), #cont)
  → text⟦, p†⟨NumberToText(GetCounter(#env))⟩†⟨JavaExprLambda(AddVar(IncCounter(#env), var:String, Formal(text⟦p†⟨NumberToText(GetCounter(#env))⟩⟧, MaybeSortAnnoToSort(#csortanno?))), #cterm[var], #cont)⟩⟧

  // fallback
  rule JavaExprLambda(#env, #cterm, #cont)
  → text⟦) -> †⟨JavaExprTerm(#env, #cterm, (env) -> text⟦)†⟨#cont(env)⟩⟧)⟩⟧

//--- Find form in sort.

func FindForm(Option<Option<Core_cdecl_sort>>, String) -> Option<Core_cform_sort>
  rule FindForm(NONE, #formname)
  → NONE

  rule FindForm(SOME(Option<Core_cdecl_sort>::NONE), #formname)
  → NONE

  rule FindForm(SOME(Option<Core_cdecl_sort>::SOME(cdecl⟦ data ##csortvars? ##CONSTRUCTOR ( ##cform* ) ⟧)), #formname)
  → PickFirst<Core_cform_sort>(#cform*, (x) -> MatchConsForm(x, #formname))

func MatchConsForm(Core_cform_sort, String) -> Bool
  rule MatchConsForm(cform⟦ ##CONSTRUCTOR ##csorts? ⟧, #formname)
  → StringEqual(#CONSTRUCTOR, #formname)

  rule MatchConsForm(#, #formname)
  → FALSE

// ----- initModule

/* Generate code initializing a module.
*/
func InitModule({ String : Core_cdecl_sort }, { String : Core_cdecl_sort }, List<Core_cqconstructor_sort>) -> Text4_text_sort

rule InitModule(#data, #func, #imports)
→ text⟦
    private static boolean initialized = false;
    public static void init(Context context) {
      if (!initialized) {
        initialized = true;
        †⟨TextFold(Map<Core_cdecl_sort Text4_text_sort>((sort)->RegisterDataSort(sort), MapValues<String Core_cdecl_sort>(#data)), text⟦⟧)⟩
        †⟨TextFold(Map<Core_cqconstructor_sort Text4_text_sort>((qcons)->RegisterImport(qcons),  #imports), text⟦⟧)⟩
      }
}⟧

func RegisterImport(eager Core_cqconstructor_sort) -> Text4_text_sort
rule RegisterImport(#name)
→ text⟦
    †⟨PathToImport(QConsToString(#name, "::"))⟩.init(context);⟧ // TODO: maybe use '.' sep

func RegisterDataSort(Core_cdecl_sort) -> Text4_text_sort
rule RegisterDataSort(cdecl⟦ data ##csortvars? ##CONSTRUCTOR ( ##cform* ) ⟧)
→ TextFold(Map<Core_cform_sort Text4_text_sort>((x) -> RegisterDataForm(x), #cform*), text⟦⟧)

func RegisterDataForm(Core_cform_sort) -> Text4_text_sort
  rule RegisterDataForm(cform⟦ ##CONSTRUCTOR ##csorts? ⟧)
  → text⟦
  context.register(†⟨Text-QuoteEscape(#CONSTRUCTOR)⟩, †⟨EValueClassName(#CONSTRUCTOR)⟩::new);⟧

  rule RegisterDataForm(cform⟦ allows-variable ⟧)
  → text⟦⟧


/* Print package declaration */
func PackageDeclaration -> Text4_text_sort
rule PackageDeclaration → PackageDeclaration2(GetEnv("javabasepackage", ""), GetEnv("javapackage", ""))

func PackageDeclaration2(String, String) -> Text4_text_sort
rule PackageDeclaration2(#base, #sub)
→ text⟦
package ⟨STRING:#base⟩†⟨PackageDeclaration3(If<String>(StringEqual(Trim(#base), ""), ()->"", ()->"."), #sub)⟩;⟧

func PackageDeclaration3(String, String) -> Text4_text_sort
rule PackageDeclaration3(#sep, #sub)
→ If<Text4_text_sort>(StringEqual(Trim(#sub), ""), ()->text⟦⟧, ()->text⟦⟨STRING:#sep⟩⟨STRING:#sub⟩⟧)

// Print java import
func PathToImport(String) -> Text4_text_sort

rule PathToImport(#name)
→ PathToImport2(If<String>(StartsWith(#name, "Std::"), ()->"org.transscript.compiler", ()->GetEnv("javabasepackage", "")), PathToDot(DownCaseFirst(#name)))

func PathToImport2(String, String) -> Text4_text_sort

rule PathToImport2(#package, #name)
→  text⟦†⟨StringToText(#package)⟩.†⟨StringToText(#name)⟩⟧

func PathToDot(String) -> String
rule PathToDot(#path) → PathToDot2(Replace(#path, "::", "."))

func PathToDot2(String) -> String
rule PathToDot2(#subpackage)
→
If<String>(Contains(#subpackage, "."),
    () -> ConcatString(ConcatString(BeforeLast(#subpackage, "."), "."), UpCaseFirst(AfterLast(#subpackage, "."))),
    () -> UpCaseFirst(#subpackage))

// Print class name
func ClassName(String) -> String
rule ClassName(#name) → UpCaseFirst(AfterLast(BeforeLast(#name, "."), "/"))

// ----------- Code generation environment

// To change when upgrade to TS.

enum VarEntry
  | Free(Text4_text_sort /* Java var */, Option<Core_csort_sort>)
  | Bound(Text4_text_sort /* Java var */, Option<Core_csort_sort>)
  | Fresh(Text4_text_sort /* Java var */, Option<Core_csort_sort>)
  | Formal(Text4_text_sort /* Java var */, Option<Core_csort_sort>)

enum MetaEntry | MetaVar(Text4_text_sort,       /* Java variable associated with the meta variable*/
                         List<Text4_text_sort>) /* Java variables associated to the meta variable bound variables */

enum JavaEnvEntry | ENum(Numeric) |  EText(Text4_text_sort) | ETexts(List<Text4_text_sort>) | EBool(Bool)
                  | EMapMeta({String : MetaEntry})
                  | EMapVar({ String : VarEntry }) | EMapDecl({ String : Core_cdecl_sort })
                  | EParams(List<Pair<Text4_text_sort Core_csort_sort>>)
                  | EContext(Text4_text_sort /* context java variable */, Numeric /* Counter */)
                  | EFnAnno(List<Core_canno_sort>)

func NewEnv -> {String : JavaEnvEntry}
rule NewEnv →
    MapPut<String JavaEnvEntry>(
    MapPut<String JavaEnvEntry>(
    MapPut<String JavaEnvEntry>(
    MapPut<String JavaEnvEntry>(
    MapPut<String JavaEnvEntry>(
    MapPut<String JavaEnvEntry>(
    MapPut<String JavaEnvEntry>(
    MapPut<String JavaEnvEntry>(
    MapPut<String JavaEnvEntry>(
    MapPut<String JavaEnvEntry>(
    MapPut<String JavaEnvEntry>(MapNew<String JavaEnvEntry>,
      "params"      , EParams(())),     /* Java method arguments. */
      "thunk"       , ETexts(())),     /* thunk arguments */
      "label"       , EText(text⟦⟧)),    /* Current pattern block label. */
      "tail"        , EBool(TRUE)),   /* Tail? */
      "meta"        , EMapMeta(MapNew<String MetaEntry>)),  /* Map meta variable to java variables */
      "vars"        , EMapVar(MapNew<String VarEntry>)),   /* Map variable to VarEntry */
      "parent"      , EText(text⟦⟧)),    /* Sub parent (if any) */
      "subindex"    , EText(text⟦⟧)),    /* Sub index */
      "binderindex" , ENum(0)),         /* Current sub binder index */
      "binders"     , ETexts(())),    /* Binders list being constructed in contraction */
      "data"        , ETexts(()))    /* List of parameters marked as data */



// --- Java printing helpers

// Print literal as a java string
func Literal(Core_cliteral_sort) -> Text4_text_sort
rule Literal(cliteral⟦ ##STRING ⟧) → text⟦stringTerm(†⟨Text-QuoteEscape(#STRING)⟩)⟧
rule Literal(cliteral⟦ ##NUMBER ⟧) → text⟦doubleTerm(⟨STRING: #NUMBER⟩)⟧

// Convert constructor to legal Java ID
func ToJavaId(String) -> Text4_text_sort
rule ToJavaId(#constructor) → Text-Mangle(UpCaseFirst(#constructor))

// Convert constructor to legal Java ID
func ToJavaIdString(String) -> Text4_text_sort
rule ToJavaIdString(#name) → Text-Mangle(#name)

// --- Helpers.

func GetValue<a>(eager {String : JavaEnvEntry}, String, (JavaEnvEntry) -> a) -> a
rule GetValue(#env, #key, #unwrap)
→ #unwrap(UnSOME<JavaEnvEntry>(MapGet<String JavaEnvEntry>(#env, #key)))

func SetValue(eager {String : JavaEnvEntry}, String, eager JavaEnvEntry) -> {String : JavaEnvEntry}
rule SetValue(#env, #key, #value)
→ MapPut<String JavaEnvEntry>(#env, #key, #value)

func UnNum(JavaEnvEntry) -> Numeric
rule UnNum(ENum(#)) → #

func UnText(JavaEnvEntry) -> Text4_text_sort
rule UnText(EText(#)) → #

func UnTexts(JavaEnvEntry) -> List<Text4_text_sort>
rule UnTexts(ETexts(#)) → #

func UnParams(JavaEnvEntry) -> List<Pair<Text4_text_sort Core_csort_sort>>
rule UnParams(EParams(#)) → #

func UnBool(JavaEnvEntry) -> Bool
rule UnBool(EBool(#)) → #

func UnMapMeta(JavaEnvEntry) -> {String : MetaEntry}
rule UnMapMeta(EMapMeta(#)) → #

func UnMapVar(JavaEnvEntry) -> { String : VarEntry }
rule UnMapVar(EMapVar(#)) → #

func UnMapDecl(JavaEnvEntry) -> { String : Core_cdecl_sort }
rule UnMapDecl(EMapDecl(#)) → #

func UnVarVar(VarEntry) -> Text4_text_sort
rule UnVarVar(Bound(#1, #2)) → #1
rule UnVarVar(Free(#1, #2)) → #1
rule UnVarVar(Fresh(#1, #2)) → #1
rule UnVarVar(Formal(#1, #2)) → #1

func UnVarSort(VarEntry) -> Option<Core_csort_sort>
rule UnVarSort(Bound(#1, #2)) → #2
rule UnVarSort(Free(#1, #2)) → #2
rule UnVarSort(Fresh(#1, #2)) → #2
rule UnVarSort(Formal(#1, #2)) → #2

func UnMetaVar(MetaEntry) -> Text4_text_sort
rule UnMetaVar(MetaVar(#1, #2)) → #1

func UnMetaArgs(MetaEntry) -> List<Text4_text_sort>
rule UnMetaArgs(MetaVar(#1, #2)) → #2

func UnContext(JavaEnvEntry) -> Text4_text_sort
rule UnContext(EContext(#1, #2)) → #1

func UnContextCount(JavaEnvEntry) -> Numeric
rule UnContextCount(EContext(#1, #2)) → #2

func UnFnAnno(JavaEnvEntry) -> List<Core_canno_sort>
rule UnFnAnno(EFnAnno(#1)) → #1

// --- Data sorts

func GetDataSorts(eager {String : JavaEnvEntry}) -> { String : Core_cdecl_sort }
rule GetDataSorts(#env) → GetValue<{ String : Core_cdecl_sort }>(#env, "datasorts", (x) -> UnMapDecl(x))

func SetDataSorts(eager {String : JavaEnvEntry}, { String : Core_cdecl_sort }) -> {String : JavaEnvEntry}
rule SetDataSorts(#env, #sorts) → SetValue(#env, "datasorts", EMapDecl(#sorts))

func IsDataSort(eager {String : JavaEnvEntry}, String) -> Bool
rule IsDataSort(#env, #cons) → HasOption<Core_cdecl_sort>(MapGet<String Core_cdecl_sort>(GetDataSorts(#env), #cons))

// --- Function sorts

func GetFuncSorts(eager {String : JavaEnvEntry}) -> { String : Core_cdecl_sort }
rule GetFuncSorts(#env) → GetValue<{ String : Core_cdecl_sort }>(#env, "funcsorts", (x) -> UnMapDecl(x))

func SetFuncSorts(eager {String : JavaEnvEntry}, { String : Core_cdecl_sort }) -> {String : JavaEnvEntry}
rule SetFuncSorts(#env, #sorts) → SetValue(#env, "funcsorts", EMapDecl(#sorts))

func IsFuncSort(eager {String : JavaEnvEntry}, String) -> Bool
rule IsFuncSort(#env, #cons) → HasOption<Core_cdecl_sort>(MapGet<String Core_cdecl_sort>(GetFuncSorts(#env), #cons))

// --- Top-level classname

func GetClassname(eager {String : JavaEnvEntry}) -> Text4_text_sort
rule GetClassname(#env) → GetValue<Text4_text_sort>(#env, "classname", (x) -> UnText(x))

func SetClassname(eager {String : JavaEnvEntry}, Text4_text_sort) -> {String : JavaEnvEntry}
rule SetClassname(#env, #classname) → SetValue(#env, "classname", EText(#classname))

// --- Step method arguments

func GetParams(eager {String : JavaEnvEntry}) -> List<Pair<Text4_text_sort Core_csort_sort>>
rule GetParams(#env) → GetValue<List>(#env, "params", (x) -> UnParams(x))

func SetParams(eager {String : JavaEnvEntry}, List<Pair<Text4_text_sort Core_csort_sort>>) -> {String : JavaEnvEntry}
rule SetParams(#env, #params) → SetValue(#env, "params",  EParams(#params))

func AddParam(eager {String : JavaEnvEntry}, Text4_text_sort, Core_csort_sort) -> {String : JavaEnvEntry}
rule AddParam(#env, #param, #csort) → SetValue(#env, "params", EParams(Append<Pair>(PairCons(#param, #csort), GetParams(#env))))

// Move to the next parameter
func NextParam(eager {String : JavaEnvEntry}) -> {String : JavaEnvEntry}
rule NextParam(#env) → SetParams(#env, Tail<Pair>(GetParams(#env)))

// Get the current parameter text
func CurrentParam(eager {String : JavaEnvEntry}) -> Text4_text_sort
rule CurrentParam(#env) → Fst<Text4_text_sort Core_csort_sort>(Head<Pair>(GetParams(#env)))

// Get the current parameter text
func CurrentParamSort(eager {String : JavaEnvEntry}) -> Core_csort_sort
rule CurrentParamSort(#env) → Snd<Text4_text_sort Core_csort_sort>(Head<Pair>(GetParams(#env)))

// --- Current method name

func GetMethodName(eager {String : JavaEnvEntry}) -> Text4_text_sort
rule GetMethodName(#env) → GetValue<Text4_text_sort>(#env, "methodname", (x) -> UnText(x))

func SetMethodName(eager {String : JavaEnvEntry}, Text4_text_sort) -> {String : JavaEnvEntry}
rule SetMethodName(#env, #name) → SetValue(#env, "methodname", EText(#name))

// --- Thunk type

func GetThunkMaker(eager {String : JavaEnvEntry}) -> Text4_text_sort
rule GetThunkMaker(#env) → GetValue<Text4_text_sort>(#env, "thunkmaker", (x) -> UnText(x))

func SetThunkMaker(eager {String : JavaEnvEntry}, Text4_text_sort) -> {String : JavaEnvEntry}
rule SetThunkMaker(#env, #type) → SetValue(#env, "thunkmaker", EText(#type))

// --- Thunk arguments

func GetThunk(eager {String : JavaEnvEntry}) -> List<Text4_text_sort>
rule GetThunk(#env) → GetValue<List>(#env, "thunk", (x) -> UnTexts(x))

func SetThunk(eager {String : JavaEnvEntry}, List<Text4_text_sort>) -> {String : JavaEnvEntry}
rule SetThunk(#env, #thunk) → SetValue(#env, "thunk", ETexts(#thunk))

func AddThunk(eager {String : JavaEnvEntry}, Text4_text_sort) -> {String : JavaEnvEntry}
rule AddThunk(#env, #thunk) → SetValue(#env, "thunk", ETexts(Append<Text4_text_sort>(#thunk, GetThunk(#env))))

// --- Rule case label

func GetLabel(eager {String : JavaEnvEntry}) -> Text4_text_sort
rule GetLabel(#env) → GetValue<Text4_text_sort>(#env, "label", (x) -> UnText(x))

func SetLabel(eager {String : JavaEnvEntry}, Text4_text_sort) -> {String : JavaEnvEntry}
rule SetLabel(#env, #label) → SetValue(#env, "label", EText(#label))

// --- Tail mode.

func GetTail(eager {String : JavaEnvEntry}) -> Bool
rule GetTail(#env) → GetValue<Bool>(#env, "tail", (x) -> UnBool(x))

func SetTail(eager {String : JavaEnvEntry}) -> {String : JavaEnvEntry}
rule SetTail(#env) → SetValue(#env, "tail", EBool(TRUE))

func UnsetTail(eager {String : JavaEnvEntry}) -> {String : JavaEnvEntry}
rule UnsetTail(#env) → SetValue(#env, "tail", EBool(FALSE))

// --- Meta

func GetMetas(eager {String : JavaEnvEntry}) -> {String : MetaEntry}
rule GetMetas(#env) → GetValue<{String:MetaEntry}>(#env, "meta", (x) -> UnMapMeta(x))

func GetMetaVar(eager {String : JavaEnvEntry}, String) -> Text4_text_sort
rule GetMetaVar(#env, #metavar) → UnMetaVar(UnSOME<MetaEntry>(MapGet<String MetaEntry>(GetMetas(#env), #metavar)))

func GetMetaArgs(eager {String : JavaEnvEntry}, String) -> List<Text4_text_sort>
rule GetMetaArgs(#env, #metavar) → UnMetaArgs(UnSOME<MetaEntry>(MapGet<String MetaEntry>(GetMetas(#env), #metavar)))

func AddMeta(eager {String : JavaEnvEntry}, String, Text4_text_sort, List<Text4_text_sort>) -> {String : JavaEnvEntry}
rule AddMeta(#env, #metavar, #term, #boundvars)
→ SetValue(#env,  "meta", EMapMeta(MapPut<String MetaEntry>(GetMetas(#env), #metavar, MetaVar(#term, #boundvars))))

// --- Variable

func GetVars(eager {String : JavaEnvEntry}) -> { String : VarEntry }
rule GetVars(#env) → GetValue<{String:VarEntry}>(#env, "vars", (x) -> UnMapVar(x))

func GetVar(eager {String : JavaEnvEntry}, String) -> Text4_text_sort
rule GetVar(#env, #var) → UnVarVar(UnSOME<VarEntry>(MapGetVar<String VarEntry String>(GetVars(#env), #var)))

func MaybeGetVar(eager {String : JavaEnvEntry}, String) -> Option<VarEntry>
rule MaybeGetVar(#env, #var) → MapGetVar<String VarEntry String>(GetVars(#env), #var)

func AddVar(eager {String : JavaEnvEntry}, String /* TODO: Core_cterm_sort */, VarEntry) -> {String : JavaEnvEntry}
rule AddVar(#env, #var, #entry)
→ SetValue(#env, "vars", EMapVar(MapPutVar<String VarEntry String>(GetVars(#env), #var, #entry)))

// --- Parent term

func GetParent(eager {String : JavaEnvEntry}) -> Text4_text_sort
rule GetParent(#env) → GetValue<Text4_text_sort>(#env, "parent", (x) -> UnText(x))

func SetParent(eager {String : JavaEnvEntry}, Text4_text_sort) -> {String : JavaEnvEntry}
rule SetParent(#env, #parent)
→ SetValue(#env, "parent",  EText(#parent))

// --- sub index

func GetSubIndex(eager {String : JavaEnvEntry}) -> Numeric
rule GetSubIndex(#env) → GetValue<Numeric>(#env, "subindex", (x) -> UnNum(x))

func SetSubIndex(eager {String : JavaEnvEntry}, Numeric) -> {String : JavaEnvEntry}
rule SetSubIndex(#env, #index)
→ SetValue(#env, "subindex", ENum(#index))

// --- sub binder index

func GetSubBinderIndex(eager {String : JavaEnvEntry}) -> Numeric
rule GetSubBinderIndex(#env) → GetValue<Numeric>(#env, "binderindex", (x) -> UnNum(x))

func SetSubBinderIndex(eager {String : JavaEnvEntry}, Numeric) -> {String : JavaEnvEntry}
rule SetSubBinderIndex(#env, #index)
→ SetValue(#env, "binderindex", ENum(#index))

func IncSubBinderIndex(eager {String : JavaEnvEntry}) -> {String : JavaEnvEntry}
rule IncSubBinderIndex(#env)
→ SetValue(#env, "binderindex", ENum(Plus(GetSubBinderIndex(#env), 1)))

// --- Binders

func GetBinders(eager {String : JavaEnvEntry}) -> List<Text4_text_sort>
rule GetBinders(#env) → GetValue<List>(#env, "binders", (x) -> UnTexts(x))

func SetBinders(eager {String : JavaEnvEntry}, List<Text4_text_sort>) -> {String : JavaEnvEntry}
rule SetBinders(#env, #binders) → SetValue(#env, "binders", ETexts(#binders))

func AddBinder(eager {String : JavaEnvEntry}, Text4_text_sort) -> {String : JavaEnvEntry}
rule AddBinder(#env, #binders) → SetValue(#env, "binders", ETexts(Append<Text4_text_sort>(#binders, GetBinders(#env))))

// --- General purpose counter

func GetCounter(eager {String : JavaEnvEntry}) -> Numeric
rule GetCounter(#env) → GetValue<Numeric>(#env, "counter", (x) -> UnNum(x))

func SetCounter(eager {String : JavaEnvEntry}, Numeric) -> {String : JavaEnvEntry}
rule SetCounter(#env, #index)
→ SetValue(#env, "counter", ENum(#index))

func IncCounter(eager {String : JavaEnvEntry}) -> {String : JavaEnvEntry}
rule IncCounter(#env)
→ SetValue(#env, "counter", ENum(Plus(GetCounter(#env), 1)))

// --- Parameters marked as data

func GetData(eager {String : JavaEnvEntry}) -> List<Text4_text_sort>
rule GetData(#env) → GetValue<List>(#env, "data", (x) -> UnTexts(x))

func AddMaybeData(eager {String : JavaEnvEntry}, Bool, Text4_text_sort) -> {String : JavaEnvEntry}
rule AddMaybeData(#env, FALSE, #term) → #env

rule AddMaybeData(#env, TRUE, #term)
→ SetValue(#env, "data", ETexts(Append<Text4_text_sort>(#term, GetData(#env))))

// --- Context

func GetContext(eager {String : JavaEnvEntry}) -> Text4_text_sort
rule GetContext(#env) → GetValue<Text4_text_sort>(#env, "context", (x) -> UnContext(x))

func GetContextCount(eager {String : JavaEnvEntry}) -> Numeric
rule GetContextCount(#env) → GetValue<Numeric>(#env, "context", (x) -> UnContextCount(x))

func SetContext(eager {String : JavaEnvEntry}, Text4_text_sort, Numeric) -> {String : JavaEnvEntry}
rule SetContext(#env, #context, #count) → SetValue(#env, "context", EContext(#context, #count))

func NewContext(eager {String : JavaEnvEntry}) -> {String : JavaEnvEntry}
rule NewContext(#env)
→ SetContext(#env, text⟦c†⟨NumberToText(GetContextCount(#env))⟩⟧, Plus(GetContextCount(#env), 1))

// --- function annotation

func GetFnAnno({String : JavaEnvEntry}) -> List<Core_canno_sort>
rule GetFnAnno(#env) → GetValue<List>(#env, "fnanno", (x) -> UnFnAnno(x))

func SetFnAnno({String : JavaEnvEntry}, List<Core_canno_sort>) -> {String : JavaEnvEntry}
rule SetFnAnno(#env, #anno*) → SetValue(#env, "fnanno", EFnAnno(#anno*))
