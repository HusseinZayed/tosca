// Copyright (c) 2015-2016 IBM Corporation.
/*
  Tosca compiler.
*/

import std::core
import std::listdef
import std::mapdef
import std::string
import std::language
import std::path
import std::text::Text4
import std::test::unit  // not used but keep it so it gets compiled.
import std::text::printer  // not used but keep it so it gets compiled.

import systemdef
import normalizer
import analyzer
import cc
import noalias
import noanno
import infer::infer
import cg::java::cgjava
import cg::cpp::cgcpp
import core::Core
import core::utils
import parser::TransScript

//--- API

/* Compile the Tosca program located to the given url, as well as imported modules
   @param `#url` a url pointing to the main program to compiler
   @param `#config` a set of configuration properties
   @return a result
 */
func Compile(#url: String, #config: {String:String}) -> Result
→ CompileS(NoAnnoS(LoadModuleS(InitSystem(#url, #config), #url, FALSE)))

// --- Compilation

/* Compile system. */
func CompileS(#system: TSystem) -> Result
→ FoldResults(Map( (mod)->CompileM(#system, mod), MapValues(GetModules(#system)) ))

/* Compile module (if not part of the standard library). */
func CompileM(#system: TSystem, #module: Module) -> Result
→ If(IsModuleCompiled(#module),
    ()->Success(()),
    ()->Debug(ConcatString("Compile", GetModuleURL(#module)),
          ()->CodeGenerateM(#system, AnalyzeCoreM(#system, NoAliasM(#system, ClosureConvertM(#system, InferM(#system, #module)))))))

func CodeGenerateM(#system: TSystem, #module: Module) -> Result
→ IfConfigDef(#system, "cpp", (v)->ToCppM(#system, #module), ()->ToJavaM(#system, #module))

/* Merge all results */
func FoldResults(#results: List<Result>) -> Result
→ FoldlP((r rs)->FoldResult(r, rs), Success(()), #results)

func FoldResult(Result, Result) -> Result
rule FoldResult(Success(#1), Success(#2)) → Success(Concat(#1, #2))
rule FoldResult(#1, #2) → FAILURE

// --- Module loading.

/* Load imported modules into the system, recursively */
func LoadImportedModules(#system: TSystem, #url:String, #imports: List<Core_cqidentifier_sort>) -> TSystem
→ FoldlP((imp system)->LoadImport(system, #url, imp), #system, #imports)

/* Load imported module into the system */
func LoadImport(#system: TSystem, #url:String, #import: Core_cqidentifier_sort) -> TSystem
→ LoadModuleS(#system, ResolveImport(#system, #url, #import), IsStandardImport(#system, #import))

/* Load module content and update system */
func LoadModuleS(#system: TSystem, #url: String, #standard:Bool) -> TSystem
→ If(HasModule(#system, #url),
     ()->#system,
     ()->LoadModuleAux(LoadCore(#system, #url, If(EndsWith(#url, ".tscc"),
                                                 ()->ParseResource("ccrsx", #url),
                                                 ()->ToCore(#url, ParseResource("transscript", Debug(#url, ()->#url)))), #standard), #url))

func LoadModuleAux(#system: TSystem, #url: String) -> TSystem
→ LoadImportedModules(#system, #url, GetImportsS(#system, #url))
