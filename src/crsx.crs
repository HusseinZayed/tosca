// Copyright (c) 2015 IBM Corporation.
//
// CRSX command line
// 
Crsx[(

$Use["system.crs"];
$Use["normalizer.crs"];
$Use["cg/cgjava.crs"];
$Use["std/core.crs"];
$Use["std/list.crs"];
$Use["std/string.crs"];

//--- API

/* Compile the Crsx program located to the given url 
   @param `$String` a url
   @return a compile Crsx program in Java (for now)
*/
Compile[$String] :: Text;  
Compile[#url] 
→ CompileSystem[InitSystem[#url]];

CompileSystem[System] :: Text;  
CompileSystem[#system] 
→ MaybeLoadModule[#system, PickNotLoaded[#system]];

MaybeLoadModule[System, Option[Module]] :: Text;

// Done loading all modules. Generate code.  
MaybeLoadModule[#system, NONE] 
→ CrsxToJava[#system];  

MaybeLoadModule[#system, SOME[Module[#url, NOTLOADED]]]
→ CompileSystem[PutModule[#system, #url, Module[#url, LoadModule[#url]]]];

LoadModule[$String] :: Content;
LoadModule[#url] 
→  Loaded[LoadCore[$[If, $[EndsWith, #url, ".crsc"], ParseCore[#url], ParseCrsx[#url]]]];

ParseCore[$String] :: Core_ccrsx_sort;
ParseCore[#url] → $[ParseResource, "ccrsx", #url];

ParseCrsx[$String] :: Core_ccrsx_sort;
ParseCrsx[#url] → ToCore[#url, $[ParseResource, "crsx", #url]];

)]