Text[(

// Fragment denoting term embedded in user language text.
//
Text-Embed[Text<!--Term-->] :: Text ;
Text-Embed[#TermText] → %n⟪†«"\u00ab\u00ab"»« #TermText »†«"\u00bb\u00bb"»⟫ ;

// Fragment denoting string embedded in user language text.
//
Text-String[Text<!--String-->] :: Text ;
Text-String[#TermText] → %n⟪†«"\u2020\u2020"»†«"\u00ab\u00ab"»« #TermText »†«"\u00bb\u00bb"»⟫ ;

// Fragment to be parsed as generated text (to be printed).
//
Text-Text[Text] :: Text ;
Text-Text[#content] → Text-User["n", #content] ;

// Concat texts with separator
Text-ConcatSep[$List[Text], Text] :: Text;
Text-ConcatSep[()             , #sep] → %n⟨⟩;
Text-ConcatSep[(#t1;)         , #sep] → %n⟨«#t1»⟩;
Text-ConcatSep[(#t1; #t2; #ts), #sep] → %n⟨«#t1»«#sep»«Text-ConcatSep[(#t2; #ts), #sep]»⟩;


// Fragment to be parsed as user language text.
//
Text-User[$String<!--ParsePrefix-->, Text<!-- Unparsed -->] :: Text ;
Text-User[#ParsePrefix, #Unparsed] →  %n⟪%†«#ParsePrefix»†«"\u27ea"»« #Unparsed »†«"\u27eb"»⟫ ;

Text-Token[$String] :: Text;
-[Data[#t]] : Text-Token[#t] → %n⟪†« #t »⟫ ; 

Text-CommentToken[$String] :: Text;
-[Data[#t]] : Text-CommentToken[#t] → %n⟪†« $[Replace,#t,"*/","*_/"] »⟫ ; 

Text-Comment[$String] :: Text;
-[Data[#t]] : Text-Comment[#t] → %n⟪†« $[:, "/*", $[Replace,#t,"*/","*_/"], "*/"] »⟫ ;

Text-QuoteEscape[$String] :: Text;
-[Data[#t]] : Text-QuoteEscape[#t] → Text-QuoteString[$[Escape,#t]];

Text-Integer[$Numeric] :: Text;
-[Data[#t]] : Text-Integer[#t] → %n⟪†« $[FormatNumber, #t] »⟫ ;

Text-Mangle[$String] :: Text;
-[Data[#t]] : Text-Mangle[#t] → %n⟪†« $[Mangle,#t] »⟫ ;


TextFold[$List[Text], Text] :: Text;
-[Discard[#sep]]: TextFold[()     , #sep] → %n⟪⟫ ;
-               : TextFold[(#1;#s), #sep] → %n⟪« #sep »« #1 »« TextFold1[#s, #sep] »⟫ ;

TextFold1[$List[Text], Text] :: Text;
-[Discard[#sep]]: TextFold1[()     , #sep] → %n⟪⟫ ;
-[Copy[#sep]]   : TextFold1[(#1;#s), #sep] → %n⟪« #sep »« #1 »« TextFold1[#s, #sep] »⟫ ;

ReverseTextFold[$List[Text], Text] :: Text;
-[Discard[#sep]]: ReverseTextFold[()        , #sep] → %n⟪⟫ ;
-[Discard[#sep]]: ReverseTextFold[(#1;)     , #sep] → %n⟪« #1 »⟫ ;
-               : ReverseTextFold[(#1;#2;#s), #sep] → %n⟪« ReverseTextFold[(#2;#s), #sep] »« #sep »« #1 »⟫ ;

// -- Conversion

StringToText[$String] :: Text ;
StringToText[#] → %n⟪†«#»⟫ ;

NumberToText[$Numeric] :: Text;
-[Data[#n]]: NumberToText[#n] → %n⟪†«$[FormatNumber,#n]»⟫;

)]