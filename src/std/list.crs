/* Copyright (c) 2015 IBM Corporation. */

/*
 * Standard library for list.
 */
List[(

$Use["std/core.crs"];
$Use["std/pair.crs"];

// Sort list definition.
//∀ a.$List ::= ( $Cons[a, $List[a]]; $Nil; );

/* 
 * Basic List Operations  
 */

/* @return `TRUE` if list is empty
 */
∀ a . IsEmpty[$List[a]] :: Boolean ;
IsEmpty[()    ] → TRUE ;
IsEmpty[(#;#s)] → FALSE ;

/* @return `TRUE` if list is not empty
 */
∀ a . IsNotEmpty[$List[a]] :: Boolean ;
IsNotEmpty[()    ] → FALSE ;
IsNotEmpty[(#;#s)] → TRUE ;


/* TRUE if list is empty
 */
∀ a b . IfEmpty[$List[a], b, b] :: b ;
IfEmpty[()    , #1, #2] → #1 ;
IfEmpty[(#;#s), #1, #2] → #2 ;

/* `Null[#XS]`
   @param `#XS` list
   @return `TRUE`  if  #XS is the empty list
           `FALSE` otherwise 
*/ 
∀ a . Null[$List[a]] :: Boolean;
Null[ () ]      → TRUE;
Null[ (#H;#T) ] → FALSE;

// TRUE is list is a singleton
∀ a. IsSingleton[$List[a]] :: Boolean ;
IsSingleton[()    ] → FALSE ;
IsSingleton[(#;#s)] → IsEmpty[#s] ;

// 
∀ a b . IfSingleton[$List[a], b, b] :: b ;
IfSingleton[()    , #1, #2] → #2 ;
IfSingleton[(#;#s), #1, #2] → IfEmpty[#s, #1, #2] ;

/* @return head of the list */
∀ a . Head[$List[a]] :: a ;
Head[(#;#s)] → # ;
Head[()] → $[Error,"Head of empty list"] ;

// Tail for the list
∀ a . Tail[$List[a]] :: $List[a] ;
Tail[(#;#s)] → #s ;
Tail[()] → $[Error,"Tail of empty list"] ;

/* Get element at given index (zero-based), or NONE
   @param `$List[a]` the list
   @param `$Numeric` the index
 */
∀ a . At[$List[a], $Numeric] :: Option[a];
At[(), #i]      → NONE;

-[Data[#i]]:
At[(#; #s), #i] → $[If, $[Equal, #i, 0], SOME[#], At[#s, $[Minus, #i, 1]]];

/*
  Concat the two list 
*/
∀ a.Concat[$List[a], $List[a]] :: $List[a] ;
Concat[(), #tail] → #tail ;
Concat[(#1;#s), #tail] → (#1 ; Concat[#s, #tail]) ;

/* `Elem[#X,#XS]`
   @param  `#X`  element
   @param  `#XS` list
   @return `TRUE`  if `#X` is element of `#XS`, i.e., occurs at least once, 
           `FALSE` otherwise 
   @note   read "#X is element of #XS to remember the order of the parameters"
*/
∀ a . Elem[ a , $List[a] ] :: Boolean;
Elem[#X,#XS] → Foldr[x y.Or[Eq[#X,x],y], FALSE, #XS];

/* `Insert[#X, #XS]`
   @param  `#X`  element
   @param  `#XS` list
   @return a new list, where `#X` is inserted at the head of `#XS`
   @note   read "insert #X in #XS to remember the order of the parameters"
*/
∀ a . Insert[a, $List[a]] :: $List[a];
Insert[#X,#XS] → (#X;#XS);

/* 
   Append element to list.
   @param  `a` element
   @param  `$List[a]` list
   @return a new list, where the element is inserted at the end of the list
*/
∀ a . Append[a, $List[a]] :: $List[a];
Append[#x,()]      → (#x;);
Append[#x,(#1;#s)] → (#1; Append[#x, #s]);

/* 
   Pick first element matching condition 
   @param  `$List[a]` list
   @return an element or NONE
*/
∀ a . PickFirst[$List[a], x::a.$Boolean] :: Option[a];
PickFirst[(), x.#[x]]      → NONE;
PickFirst[(#1;#s), x.#[x]] → PickFirst1[#[#1], #1, #s, x.#[x]];

∀ a . PickFirst1[$Boolean, a, $List[a], x::a.$Boolean] :: Option[a];
PickFirst1[$True, #1, #s, x.#[x]]  → SOME[#1];
PickFirst1[$False, #1, #s, x.#[x]] → PickFirst[#s, x.#[x]];

/* `ListEqual[x y.#C[x,y], #XS, #YS]`
   @param  `#C`  a condition to check for equality 
   @param  `#XS` list
   @param  `#YS` list
   @return `TRUE` if the elements of `#XS` are equal to the elements of `#YS` 
           wrt the supplied condition for equality `#C`
*/
∀ a b. ListEqual[ x:: a y :: b . Boolean, $List[a], $List[b] ] :: Boolean;
ListEqual[ x y.#C[x,y], (),       ()] → TRUE;
ListEqual[ x y.#C[x,y], (#x;#xs), ()] → FALSE;
ListEqual[ x y.#C[x,y], (), (#y;#ys)] → FALSE;
ListEqual[ x y.#C[x,y], (#x;#xs), (#y;#ys)] → And[ #C[#x,#y], ListEqual[ x y.#C[x,y], #xs, #ys]] ;

/* `ConditionalInsert[#Condition, #X, #XS]`
   @param  `#Condition` condition for inserting the element
   @param  `#X`         element
   @param  `#XS`        list
   @return if `#Condition` evaluates to TRUE it calls Insert[`#X`,`#XS`]  
           otherwise `#XS`
*/
∀ a . ConditionalInsert[Boolean,a,$List[a]] :: $List[a] ;
ConditionalInsert[TRUE, #X,#XS] → Insert[#X,#XS];
ConditionalInsert[FALSE,#X,#XS] → #XS;

/* `ContainsDuplicates[#XS]`
   @param `#XS`   list
   @return `TRUE` if `#XS` contains duplicate elements, `FALSE` otherwise 
*/
∀ a . ContainsDuplicates[$List[a]] :: Boolean;
ContainsDuplicates[ ()     ] → FALSE;
ContainsDuplicates[ (#H;#T)] → Or[Elem[#H,#T],ContainsDuplicates[#T]];

/* `RemoveDuplicates[#XS]`
   @param  `#XS` list
   @return a duplicate free version of `#XS`
*/
∀ a . RemoveDuplicates[ $List[a]] :: $List[a];
RemoveDuplicates[ () ] → ();
RemoveDuplicates[ (#X;#XS) ] → Insert[ #X, RemoveDuplicates[Filter[ x.Not[Eq[x,#X]], #XS]]];

/* `Length[#XS]`
   @param  `#XS` list
   @return length of `#XS` 
*/
∀ a . Length[$List[a]] :: $Numeric;
Length[#XS] → IWrapFoldlP[x y.NumberPlus[1,y],0,#XS];

/* `Intersect[#XS, #YS]`
   @param  `#XS` list
   @param  `#YS` list
   @return the intersection of `#XS` and `#YS`
   @note   if the first list contains duplicates, the resulting list will as well
           but not vice versa
*/
∀ a . Intersect[$List[a], $List[a]] :: $List[a];
Intersect[#XS, #YS] → Foldr[x zs.ConditionalInsert[Elem[x,#YS],x,zs],(),#XS] ;

/* `Union[#XS, #YS]`
   @param  `#XS` list
   @param  `#YS` list
   @return the union of `#XS` and `#YS`, in particular the duplicates are removed
*/
∀ a . Union[$List[a], $List[a]] :: $List[a];
Union[#XS, #YS] → RemoveDuplicates[Concat[#XS,#YS]];

/* `Reverse[#XS]`
   @param  `#XS` list
   @return the reverse list of `#XS` 
*/
∀ a . Reverse[$List[a]] :: $List[a];
Reverse[#XS] → Foldl[ x xs. Insert[x,xs], (), #XS];

/* 
 * Zip
 */

/* `Zip[#XS,#YS]`
   @param  `#XS` list
   @param  `#YS` list
   @return a list of Pairs where the first argument is taken from `#XS` and the second argument from `#YS`. 
   @error: if `Length[#XS] != Length[#YS]`
*/
∀ a b . Zip[$List[a], $List[b]] :: $List[Pair[a,b]];
Zip[() ,      () ]       → ();
Zip[() ,      #  ]       → $[Error,"ListExtension.Zip: second argument list was longer"];
Zip[ # ,      () ]       → $[Error,"ListExtension.Zip: first argument list was longer"];
Zip[(#X;#XS), (#Y;#YS) ] → ( PairCons[#X,#Y] ; Zip[#XS,#YS]);

/* `LookUp[ #Key, #KeyValueList]`
    @param `#Key`          a key
    @param `#KeyValueList` a list of key-value pairs
    @return a list of values associated to the `#Key` in `#KeyValueList` and returns the empty list, if `#Key` was not found  
*/
∀ a b. LookUp[a, $List[Pair[a,b]]] :: $List[b];
-[ Data[#KeyValueList] ] :
LookUp[ #Key, #KeyValueList] → Map[ x.Snd[x], Filter[pair.Eq[#Key, Fst[pair]], #KeyValueList]];

/* DelEntry
   @param  `#Key`          of the key-value pairs to delete
   @param  #KeyValueist` the key-value pairs
   @return a list of `#KeyValueList`, where all entries with `#Key` are deleted
 */
∀ a b . DelEntry[ a , $List[Pair[a,b]] ] ::  $List[Pair[a,b]] ;
DelEntry[#Key, #KeyValueList] → Filter[ pair. Not[Eq[Fst[pair], #Key]], #KeyValueList];

/* `Partition[x.#C[x], #XS]`
    @param `x.#C[x]` a condition to partition
    @param `XS`      the list to partition
    @return a pair `PairCons[#Ts, #Fs]`, where the first component `#Ts` holds all 
           elements of `#XS` for which `#C` evaluates to `TRUE` and the second compoent `#Fs`
           holds all elements for `#C` evaluates to `FALSE` 
*/

∀ a . Partition[ x :: a . Boolean, $List[a]] :: Pair[$List[a],$List[a]];
Partition[x.#C[x], #XS] → Foldr[ x res . MatchPartition[#C[x], x, res ], PairCons[(),()], #XS];

∀ a . MatchPartition[Boolean, a, Pair[$List[a],$List[a]]] :: Pair[$List[a],$List[a]];
MatchPartition[TRUE,  #X, PairCons[#TS, #FS]] → PairCons[Insert[#X,#TS], #FS];
MatchPartition[FALSE, #X, PairCons[#TS, #FS]] → PairCons[#TS, Insert[#X,#FS]];

/* Apply each item in list to function
 */
∀ a b . Map[x::a.$sort[b], $List[a]] :: $List[b] ;
Map[x.#[x], ()] → () ;
Map[x.#[x], (#1;#s)] → (#[#1]; Map[x.#[x], #s]) ;

/* `Map2[x.#F[x], #XS]`
   @param   `x.#F[x]` a function which takes one element of the list as input
   @param   `#XS`     a list of elements
   @returns a list where the function `#F` is applied to all elements `#X` in `#XS`
   @note    alternative implementation to `Map` based on `Foldr`
*/
∀ a b. Map2[x::a.$sort[b], $List[a]] :: $List[b];
Map2[x.#F[x], #XS] → Foldr[y ys.Insert[#F[y],ys], (), #XS];

/* Monadic map. Apply each item in list to the function taking a state and an element,
   evaluating to a new state and a value. Apply the next element with the new state.
   @param `state::a x::b.Pair[a, c]` the function to apply
   @param `a` the initial state
   @param `$List[b]` the list of elements to apply
   @return a pair containing the end state and the a list containing resulting values.
 */
∀ a b c. MMap[state::a x::b.Pair[a, c], a, $List[b]] :: Pair[a, $List[c]];
MMap[state x.#[state, x], #state, #s] → MMap2[state x.#[state, x], #state, #s, ()];

∀ a b c. MMap2[state::a x::b.Pair[a, c], a, $List[b], $List[c]] :: Pair[a, $List[c]];
MMap2[state x.#[state, x], #state, (), #rs]      → PairCons[#state, #rs];
MMap2[state x.#[state, x], #state, (#1;#s), #rs] → MMap3[#[#state, #1], state x.#[state, x], #s, #rs];

∀ a b c. MMap3[Pair[a, c], state::a x::b.Pair[a, c], $List[b], $List[c]] :: Pair[a, $List[c]];
MMap3[PairCons[#nstate, #r], state x.#[state, x], #s, #rs] → MMap2[state x.#[state, x], #nstate, #s, Append[#r, #rs]];

// 
∀ a.Cont[a] ::= ( CONT[ok¹::OK_SORT.$sort[a]];  );

/* `{#}ApplyCont[#cont]`
   @param #cont continutation to apply
 */
∀ a. ApplyCont[Cont[a]] :: a;
{#E}ApplyCont[CONT[ok¹.#[ok¹]]] → {#E}#[OK];

/* `SeqMap[x c.#[x,c], #L, ok.#D[ok]]`
    Apply each item in list #L to function #, as a continuation, passing the environment along
 */
∀ a b. {$String:$Any}SeqMap[x::a c::Cont[b]. $sort[b], $List[a], ok::OK_SORT.$sort[b]] :: b ;
{#E}SeqMap[x c.#[x, c], #list, ok¹.#done[ok¹]] → {#E}SeqMap1[OK, x c.#[x, c], #list, ok¹.#done[ok¹]];

// Internal
∀ a b. {$String:$StringEntrySort} SeqMap1[OK_SORT, x::a c::Cont[b]. $sort[b], $List[a], ok¹::OK_SORT.$sort[b]] :: b ;
{#E}SeqMap1[OK, x c.#[x, c], ()     , ok¹.#done[ok¹]] → {#E}#done[OK];
{#E}SeqMap1[OK, x c.#[x, c], (#1;#s), ok¹.#done[ok¹]] → {#E}#[#1, CONT[ok¹.SeqMap1[ok¹, x c.#[x, c], #s,  ok¹.#done[ok¹]]]];

/* `ThenSeqMap[ok, x c.#[x,c], #L, ok.#D[ok]]`
   Delay variant of `SeqMap`
 */
∀ a b. {$String:$Any}ThenSeqMap[OK_SORT, x::a c::Cont[b]. $sort[b], $List[a], ok::OK_SORT.$sort[b]] :: b ;
{#E}ThenSeqMap[OK, x c.#[x, c], #list, ok¹.#done[ok¹]] → {#E}SeqMap[x c.#[x, c], #list, ok¹.#done[ok¹]];

/* 
 * Filter 
 */

/* `Filter[x.#C[x], #XS]`
    @param  `x.#C[x]` a function which takes an element of the list as input and returns a `Boolean` 
    @param  `#XS`     a list
    @return a list, where with only those `#X` in `#XS` where condition `#C[#X]` evaluates to `TRUE`
*/
∀ a . Filter[ x :: a . Boolean, $List[a]] :: $List[a];
Filter[ x.#C[x], #XS] → Foldr[ x y . ConditionalInsert[#C[x],x,y] ,(),#XS];

/* 
 * Folds 
 */

/* `Foldl[x y.#F[x,y], #Z, #XS]`
    @param  `x y.#F[x, y]` a function which takes as first argument an element of `#XS` and `#Z` as a second.
    @param  `#Z`           the starting value of the accumulator
    @param  `#XS`          a list   
    @return a value, where the the function `#F` is applied left associatively to all `#X` in `#XS` starting from the accumulator `#Z`
*/
∀ a b . Foldl[x :: a  y :: b . $sort[b], b, $List[a]] :: b;
Foldl [x y. #F[x,y], #Z, ()] → #Z;
Foldl [x y. #F[x,y], #Z, (#H; #T)] → Foldl[x y.#F[x,y], #F[#H,#Z], #T];

/* 
  @note `InterpreterWrap` for `FoldP` enforcing computation of argument list `#XS` 
*/
∀ a b . IWrapFoldlP[x :: a  y :: b . $sort[b], b, $List[a]] :: b;
-[Data[#XS]] : IWrapFoldlP[x y.#F[x,y], #Z, #XS] → FoldlP[x y.#F[x,y], #Z, #XS];

/* `FoldlP[x y.#F[x,y], #Z, #XS]`
    @param  `x y.#F[x, y]` a function which takes as first argument an element of `#XS` and `#Z` as a second.
    @param  `#Z`           the starting value of the accumulator
    @param  `#XS`          a list   
    @return  a list, where the the function `#F` is applied left associatively to all `#X` in `#XS` starting from the accumulator `#Z`, which is evaluated at each intermediate step
    @note    as `Foldl` but forces intermediate data computation 
*/
∀ a b . FoldlP [x :: a  y :: b . $sort[b], b, $List[a]] :: b;
-[Data[#Z]] : FoldlP [x y. #F[x,y], #Z, ()] → #Z;
-[Data[#Z]] : FoldlP [x y. #F[x,y], #Z, (#H; #T)] → FoldlP[x y.#F[x,y], #F[#H,#Z], #T];


/* `Foldr[x y.#F[x,y], #Z, #XS]: 
    @param  `x y.#F[x, y]` a function which takes as first argument an element of `#XS` and `#Z` as a second.
    @param  `#Z`           the starting value of the accumulator
    @param  `#XS`          a list   
    @return a list, where the the function `#F` is applied right associatively to all `#X` in `#XS` starting from the accumulator `#Z`
    @note like Foldl, but right associative 
*/
∀ a b . Foldr[x :: a  y :: b . $sort[b], b, $List[a]] :: b;
Foldr [x y. #F[x,y], #Z, ()] → #Z;
Foldr [x y. #F[x,y], #Z, (#H; #T)] → #F[#H,Foldr[x y. #F[x,y], #Z, #T]];


)]