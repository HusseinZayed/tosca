/* Copyright (c) 2014 IBM Corporation. */
/*
 * Core CRSX API.
 */
Core[(

$Lax;

OK_SORT ::= ( OK; );

// --- Builtin string entry

$StringEntry ::= ( STRING[$String];);

// --- Optional value

∀ a.Option[a] ::= ( SOME[a]; NONE; );

∀ a.UnSOME[Option[a]] :: a;
UnSOME[SOME[#]] → #;
UnSOME[NONE]    → $[Error, "Error: no optional value."];

// --- Boolean

Boolean ::=( TRUE; FALSE; );

∀ a . If[Boolean, a, a] :: a ;
If[TRUE,  #1, #2] → #1 ;
If[FALSE, #1, #2] → #2 ;

And[Boolean, Boolean] :: Boolean ;
And[TRUE, #]  → # ;
And[FALSE, #] → FALSE ;

Or[Boolean, Boolean] :: Boolean ;
Or[TRUE, #]  → TRUE;
Or[FALSE, #] → #;

// Weird rule because crsx3 interpreter does not handle boolean equality properly
WrapBoolean[$Boolean] :: Boolean ;
-[Data[#]]: WrapBoolean[#]  → $[If, $[DeepEqual, $[Show, #], "$True"], TRUE, FALSE];

UnwrapBoolean[Boolean] :: $Boolean ;
UnwrapBoolean[TRUE]  → $True;
UnwrapBoolean[FALSE] → $False;

// --- Debugging

∀ a . Trace[a] :: a;
-[Data[#]]: Trace[#] → $[Trace, #];

)]