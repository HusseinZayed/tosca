// Copyright (c) 2016 IBM Corporation.

import Std::Core
import Std::Mapdef
import Std::Listdef
import Std::Pairdef
import Std::Num
import Core::Core
import Systemdef

// --- Sort inference environment

/* Environment entry sort */
enum EnvEntry | EContent(LoadedContent) // read-only
              | ESystem(TSystem)        // read-only
              | ETerm(Core_cterm_sort)
              | ESort(Core_csort_sort)
              | EBool(Bool)
              | ENum(Numeric)
              | EMapMeta({String : Core_csort_sort})
              | EMapSort({String : Core_csort_sort}) // Sort enviroment (usually called theta)
              | ESortArgs(List<List<Pair<String String>>>) // sort variable to generic sort argument

/* Corresponding unwrappers */
func UnContent(EnvEntry) -> LoadedContent
rule UnContent(EContent(#)) → #

func UnSystem(EnvEntry) -> TSystem
rule UnSystem(ESystem(#)) → #

func UnTerm(EnvEntry) -> Core_cterm_sort
rule UnTerm(ETerm(#)) → #

func UnBool(EnvEntry) -> Bool
rule UnBool(EBool(#)) → #

func UnNum(EnvEntry) -> Numeric
rule UnNum(ENum(#)) → #

func UnSort(EnvEntry) -> Core_csort_sort
rule UnSort(ESort(#)) → #

func UnMapSort(EnvEntry) -> {String : Core_csort_sort}
rule UnMapSort(EMapSort(#)) → #

func UnSortArgs(EnvEntry) -> List<List<Pair<String String>>>
rule UnSortArgs(ESortArgs(#)) → #


/* Initialize environment */
func EnvNew(#content:LoadedContent, #system:TSystem) -> {String:EnvEntry}
→ SetInPattern(SetSystem(SetContent(MapNew<String EnvEntry>, #content), #system), TRUE)

// --- Module content

func SetContent(#env: {String : EnvEntry}, #content: LoadedContent) -> {String : EnvEntry}
→ SetValue(#env, "content", EContent(#content))

func GetContent(#env: {String : EnvEntry}) -> LoadedContent
→ UnContent(GetValue(#env, "content"))

// --- System

func SetSystem(#env: {String : EnvEntry}, #system: TSystem) -> {String : EnvEntry}
→ SetValue(#env, "system", ESystem(#system))

func GetSystem(#env: {String : EnvEntry}) -> TSystem
→ UnSystem(GetValue(#env, "system"))

// --- InPattern?

func SetInPattern(#env: {String : EnvEntry}, #inpattern: Bool) -> {String : EnvEntry}
→ SetValue(#env, "inpattern", EBool(#inpattern))

func GetInPattern(#env: {String : EnvEntry}) -> Bool
→ UnBool(GetValue(#env, "inpattern"))

// --- Stack of sort variable to sort variable argument mappings

/* Reset mapping stack */
func ResetSortArgs(#env: {String : EnvEntry}) -> {String : EnvEntry}
→ SetValue(#env, "sortargs", ESortArgs(()))

func GetSortArgs(#env: {String : EnvEntry}) -> List<List<Pair<String String>>>
→ UnSortArgs(GetValue(#env, "sortargs"))

/* Push the given mapping onto the stack  */
func PushSortArgs(#env: {String : EnvEntry}, #mapping: List<Pair<String String>>) -> {String : EnvEntry}
→ SetValue(#env, "sortargs", ESortArgs(Append<List>(#mapping, GetSortArgs(#env))))

func PopSortArgs(#env:{String : EnvEntry}) -> {String : EnvEntry}
→ SetValue(#env, "sortargs", ESortArgs(Tail<List>(GetSortArgs(#env))))

func TopSortArgs(#env:{String : EnvEntry}) -> List<Core_csortargs_sort>
→ csortargs?⟦ < ⟨csort*: Map<Pair Core_csort_sort>((pair)->csort⟦ ⟨VARIABLE: Snd<String String>(pair)⟩ ⟧, Head<List>(GetSortArgs(#env)))⟩ > ⟧

func GetSortArg(#env: {String : EnvEntry}, #sortvar:String) -> Option<String>
→ HeadO<String>(LookUp<String String>(#sortvar, Head<List>(GetSortArgs(#env))))

// --- Sort argument counter to generate unique sort argument name

func ResetCounter(#env: {String : EnvEntry}) -> {String : EnvEntry}
→ SetCounter(#env, 1)

func GetCounter(#env: {String : EnvEntry}) -> Numeric
→ UnNum(GetValue(#env, "counter"))

func SetCounter(#env: {String : EnvEntry}, #num: Numeric) -> {String : EnvEntry}
→ SetValue(#env, "counter", ENum(#num))

func IncCounter(#env: {String : EnvEntry}) -> {String : EnvEntry}
→ SetValue(#env, "counter", ENum(Plus(GetCounter(#env), 1)))

// --- Sort environment

/* Reset sort environment */
func ResetSorts(#env: {String : EnvEntry}) -> {String : EnvEntry}
→ SetValue(#env, "sorts", EMapSort(MapNew<String Core_csort_sort>))

/* @return the sort environment */
func GetSorts(#env: {String : EnvEntry}) -> {String:Core_csort_sort}
→ UnMapSort(GetValue(#env, "sorts"))

/* @return the sort associated to the given sort variable */
func GetSort(#env: {String : EnvEntry}, #sortvar: String) -> Option<Core_csort_sort>
→ MapGet<String Core_csort_sort>(GetSorts(#env), #sortvar)

/* Add sort variable sort pair to the sort environment. Silently overrides the previous entry (if any) */
func PutSort(#env: {String : EnvEntry}, #sortvar: String, #sort: Core_csort_sort) -> {String : EnvEntry}
→ SetValue(#env, "sorts", EMapSort(MapPut<String Core_csort_sort>(GetSorts(#env), #sortvar, #sort)))

// --- Some generic helper functions

func GetValue(#env: {String : EnvEntry}, #key: String) -> EnvEntry
→ UnSOME<EnvEntry>(MapGet<String EnvEntry>(#env, #key))

func SetValue(#env: {String : EnvEntry}, #key: String, #value: EnvEntry) -> {String : EnvEntry}
→ MapPut<String EnvEntry>(#env, #key, #value)
