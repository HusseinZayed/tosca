// Copyright (c) 2016 IBM Corporation.

import Std::Core
import Std::Mapdef
import Std::Listdef
import Std::Pairdef
import Core::Core

/*
  Substitute with extensions. The following substitutions are supported:
  - variables are substituted by their associated term.
  - meta-variables are substituted by their associated meta-variable (like renaming)
*/
func Substitute(Core_cterm_sort, {String : Core_cterm_sort}) -> Core_cterm_sort
rule Substitute(cterm⟦ ##cqconstructor ##csortargs? ##cterms? ##csortanno? ⟧, #subst) → cterm⟦ ##cqconstructor ##csortargs? ⟨cterms?: SubstMaybeTerms(#cterms?, #subst)⟩ ##csortanno? ⟧
rule Substitute(cterm⟦ ##METAVAR ##cterms? ##csubst? ##csortanno?          ⟧, #subst) → cterm⟦ ⟨METAVAR: SubstMetaVar(#METAVAR, #subst)⟩ ⟨cterms?: SubstMaybeTerms(#cterms?, #subst)⟩ ⟨csubst?: SubstMaybeSubst(#csubst?, #subst)⟩ ##csortanno? ⟧
rule Substitute(cterm⟦ #cliteral                                           ⟧, #subst) → cterm⟦ #cliteral ⟧

// TODO: replace when issue #20 is fixed
//rule Substitute(cterm⟦ ##VARIABLE ##csortanno?                             ⟧, #subst) → cterm⟦ ⟨VARIABLE: SubstVariable(#VARIABLE, #subst)⟩ ##csortanno? ⟧
rule Substitute(cterm⟦ ##VARIABLE ##csortanno?                             ⟧, #subst) → SubstVariable(#VARIABLE, #csortanno?, #subst)

rule Substitute(cterm⟦ { ##cmapentries? }                                  ⟧, #subst) → cterm⟦ { ⟨cmapentries?: SubstMaybeMapEntries(#cmapentries?, #subst)⟩ } ⟧
rule Substitute(cterm⟦ [ x ##csortanno ] ##cterm                           ⟧, #subst) → cterm⟦ [ x ##csortanno ] ⟨cterm: Substitute(#cterm, #subst)⟩ ⟧
rule Substitute(cterm⟦ ( f ##csortanno ) ##cterm                           ⟧, #subst) → cterm⟦ ( f ##csortanno ) ⟨cterm: Substitute(#cterm, #subst)⟩ ⟧
rule Substitute(cterm⟦ thunk ##cterm                                       ⟧, #subst) → cterm⟦ thunk ⟨cterm: Substitute(#cterm, #subst)⟩ ⟧

func SubstMaybeTerms(List<Core_cterms_sort>, {String : Core_cterm_sort}) -> List<Core_cterms_sort>
rule SubstMaybeTerms(cterms?⟦              ⟧, #subst) → cterms?⟦ ⟧
rule SubstMaybeTerms(cterms?⟦ (          ) ⟧, #subst) → cterms?⟦ () ⟧
rule SubstMaybeTerms(cterms?⟦ ( ##cterm* ) ⟧, #subst) → cterms?⟦ ( ⟨cterm*: SubstTerms(#cterm*, #subst)⟩ ) ⟧

func SubstMaybeSubst(List<Core_csubst_sort>, {String : Core_cterm_sort}) -> List<Core_csubst_sort>
rule SubstMaybeSubst(csubst?⟦              ⟧, #subst) → csubst?⟦ ⟧
rule SubstMaybeSubst(csubst?⟦ [ ##cterm* ] ⟧, #subst) → csubst?⟦ [ ⟨cterm*: SubstTerms(#cterm*, #subst)⟩ ] ⟧

func SubstTerms(List<Core_cterm_sort>, {String : Core_cterm_sort}) -> List<Core_cterm_sort>
rule SubstTerms(cterm*⟦                  ⟧, #subst) → cterm*⟦ ⟧
rule SubstTerms(cterm*⟦ ##cterm ##cterm* ⟧, #subst) → cterm*⟦ ⟨cterm: Substitute(#cterm, #subst)⟩ ⟨cterm*: SubstTerms(#cterm*, #subst)⟩ ⟧

func SubstMetaVar(#metavar: String, #subst: {String : Core_cterm_sort}) -> String
→ IfPresent<Core_cterm_sort String>(MapGet<String Core_cterm_sort>(#subst, #metavar), (substitute) -> SubstGetMetaVar(substitute), ()->#metavar)

func SubstGetMetaVar(Core_cterm_sort) -> String
rule SubstGetMetaVar(cterm⟦ ##METAVAR ⟧) → #METAVAR
rule SubstGetMetaVar(#cterm) → Error<String>("Fatal Error: invalid meta-variable substitute. Only meta-variables are allowed.")

// TODO: #VARIABLE because of issue #20
func SubstVariable(#VARIABLE: String, #csortanno?: List<Core_csortanno_sort>, #subst: {String : Core_cterm_sort}) -> Core_cterm_sort
→ IfPresent<Core_cterm_sort Core_cterm_sort>(MapGetVar<String Core_cterm_sort String>(#subst, #VARIABLE), (substitute) -> substitute, ()->cterm⟦ ##VARIABLE ##csortanno? ⟧)

func SubstMaybeMapEntries(List<Core_cmapentries_sort>, {String : Core_cterm_sort}) -> List<Core_cmapentries_sort>
rule SubstMaybeMapEntries(cmapentries?⟦              ⟧, #subst) → cmapentries?⟦⟧
rule SubstMaybeMapEntries(cmapentries?⟦ ##cmapentry* ⟧, #subst) → cmapentries?⟦ ⟨cmapentry*: SubstMapEntries(#cmapentry*, #subst)⟩ ⟧

func SubstMapEntries(List<Core_cmapentry_sort>, {String : Core_cterm_sort}) -> List<Core_cmapentry_sort>
rule SubstMapEntries(cmapentry*⟦                          ⟧, #subst) → cmapentry*⟦⟧
rule SubstMapEntries(cmapentry*⟦ ##cmapentry ##cmapentry* ⟧, #subst) → cmapentry*⟦ ⟨cmapentry: SubstMapEntry(#cmapentry, #subst)⟩ ⟨cmapentry*: SubstMapEntries(#cmapentry*, #subst)⟩ ⟧

func SubstMapEntry(Core_cmapentry_sort, {String : Core_cterm_sort}) -> Core_cmapentry_sort
rule SubstMapEntry(cmapentry⟦ : ##METAVAR ⟧, #subst) → cmapentry⟦ : ⟨METAVAR: SubstMetaVar(#METAVAR, #subst)⟩ ⟧
//  DO THE REST WHEN REAL SUPPORT FOR MAP

/* Sort variable substitution */
func SubstSort(Core_csort_sort, List<Pair<String Core_csort_sort>>) -> Core_csort_sort
rule SubstSort(csort⟦ ##CONSTRUCTOR ##csorts? ⟧, #subst) → csort⟦ ##CONSTRUCTOR ⟨csorts?: SubstSortMaybeSorts(#csorts?, #subst)⟩ ⟧
rule SubstSort(csort⟦ ##VARIABLE              ⟧, #subst) → IfPresent<Core_csort_sort Core_csort_sort>(HeadO<Core_csort_sort>(LookUp<String Core_csort_sort>(#VARIABLE, #subst)), (sort)->sort, ()->csort⟦ ##VARIABLE ⟧)
rule SubstSort(csort⟦ [ ##csort1 ] ##csort2   ⟧, #subst) → csort⟦ [ ⟨csort: SubstSort(#csort1, #subst)⟩ ] ⟨csort: SubstSort(#csort2, #subst)⟩ ⟧
rule SubstSort(csort⟦ ( ##csort1 ) ##csort2   ⟧, #subst) → csort⟦ ( ⟨csort: SubstSort(#csort1, #subst)⟩ ) ⟨csort: SubstSort(#csort2, #subst)⟩ ⟧
rule SubstSort(csort⟦ { ##cmapsort* }         ⟧, #subst) → csort⟦ { ⟨cmapsort*: SubstMapSorts(#cmapsort*, #subst)⟩ } ⟧
rule SubstSort(csort⟦ thunk ##csort           ⟧, #subst) → csort⟦ thunk ⟨csort: SubstSort(#csort, #subst)⟩ ⟧
rule SubstSort(csort⟦ data ##csort            ⟧, #subst) → csort⟦ data ⟨csort: SubstSort(#csort, #subst)⟩ ⟧

func SubstSortMaybeSorts(List<Core_csorts_sort>, List<Pair<String Core_csort_sort>>) -> List<Core_csorts_sort>
rule SubstSortMaybeSorts(csorts?⟦             ⟧, #subst) → csorts?⟦⟧
rule SubstSortMaybeSorts(csorts?⟦ ( ##csort*) ⟧, #subst) → csorts?⟦ ( ⟨csort*: SubstSortSorts(#csort*, #subst)⟩ ) ⟧

func SubstSortSorts(List<Core_csort_sort>, List<Pair<String Core_csort_sort>>) -> List<Core_csort_sort>
rule SubstSortSorts(csort*⟦ ##csort ##csort*  ⟧, #subst) → csort*⟦ ⟨csort: SubstSort(#csort, #subst)⟩ ⟨csort*: SubstSortSorts(#csort*, #subst)⟩ ⟧
rule SubstSortSorts(csort*⟦                   ⟧, #subst) → csort*⟦⟧

func SubstMapSorts(List<Core_cmapsort_sort>, List<Pair<String Core_csort_sort>>) -> List<Core_cmapsort_sort>
rule SubstMapSorts(cmapsort*⟦ ##csort1 : ##csort2 ##cmapsort* ⟧, #subst) → cmapsort*⟦ ⟨csort: SubstSort(#csort1, #subst)⟩ : ⟨csort: SubstSort(#csort2, #subst)⟩ ⟨cmapsort*: SubstMapSorts(#cmapsort*, #subst)⟩  ⟧
rule SubstMapSorts(cmapsort*⟦                                 ⟧, #subst) → cmapsort*⟦ ⟧
