// Copyright (c) 2015-2016 IBM Corporation.

/**
 * Main Gradle build.
 *
 * The default tasks is install, which creates a candidate Tosca distibution file in build.
 *
 * By default, the distribution is created from Tosca source files compiled using a previous Tosca stable build.
 *
 * There are various ways of overriding the default behavior by specifying -P option on the gradle command line.
 * These options are:
 * - dev : use latest Tosca class files stored in targets/java/build
 * - crsx3: use crsx3 source files
 */

import org.transscript.gradle.*

defaultTasks 'install'

apply plugin:'base'

project.ext {

// Change to true when changes are made to the runtime affecting generated parsers
// See also settings.gradle
nparsers = false

}

// -- Configure build script which requires Tosca gradle plugin
buildscript {
    repositories {
        flatDir { dirs 'gradle' } // to find transscript-gradle-plugin.jar
    }
    dependencies {
        classpath 'org.transscript.gradle:transscript-gradle-plugin:1.+'
    }
}

apply plugin:'org.transscript.gradle'

repositories {
    flatDir { dirs 'targets/java/dist' } // where to find Tosca jar
    flatDir { dirs 'targets/java/dist/lib' }  // where to find crsx3.jar
    mavenCentral() // Where to find ANTLR
}

dependencies {
    transscript project.hasProperty('dev') ? 'org.transscript:transscript:1.0.0-SNAPSHOT@jar' : 'org.transscript:transscript:1.0.0-ALPHA@jar'
    transscript 'org.crsx:crsx3@jar'
    transscript 'org.antlr:antlr4:4.5'
}

// --- Generate all meta and term parsers

task coreParsers(type: PG4Task) {
    description "Generate Core term and meta parsers"
    source = file('src/core/Core.g4')
    generatedFileDir = file('src/core')
    metaPrefix = '##'
	ncg = true
    locsort = 'Region({})'
}

task coreParsers2(type: TransScriptTask) {
	description = "Generate Core term and meta parsers"
	command = "pg"
	dependencies = fileTree(dir: 'src/core', include: ['Core.g4'])
	sources     = fileTree(dir: 'src/core', include: ['Core.g4'])
	outputDir   = file('src/core2/')
	bootparserpath =  "file:parsers/build/classes/main/" // override Tosca parsers location. MUST END WITH /
	metaprefix = '##'
	location   = 'Region({})'
}


task toscaParsers(type: PG4Task) {
    description 'Generate Tosca term and meta parsers'
    source = file('src/parser/TransScript.g4')
    generatedFileDir = file('src/parser')
    metaPrefix = '##'
	ncg =  true
	//locsort = 'Region({})'
}

task toscaParsers2(type: TransScriptTask) {
    description 'Generate Tosca term and meta parsers'
    command = "pg"
    dependencies = fileTree(dir: 'src/parser', include: ['TransScript.g4'])
    sources     = fileTree(dir: 'src/parser', include: ['TransScript.g4'])
    outputDir   = file('src/parser2/')
    bootparserpath =  "file:parsers/build/classes/main/" // override Tosca parsers location. MUST END WITH /
    metaprefix = '##'
//    location   = 'Region({})'
}

task textParsers(type: PG4Task) {
    description 'Generate Text term and meta parsers'
    source = file('src/std/text/Text4.g4')
    generatedFileDir = file('src/std/text')
    defaultRule = 'text'
	ncg = true
	notext = true
}

task textParsers2(type: TransScriptTask) {
    description 'Generate Text term and meta parsers'
    command = "pg"
    dependencies = fileTree(dir: 'src/std/text', include: ['Text4.g4'])
    sources     = fileTree(dir: 'src/std/text', include: ['Text4.g4'])
    outputDir   = file('src/std/text2/')
    bootparserpath =  "file:parsers/build/classes/main/" // override Tosca parsers location. MUST END WITH /
    defaultrule = 'text'
	  notext = true
}

// --- dependencies

// --- To make sure we don't delete something important by accident
task cleanCoreParsers(overwrite : true) {}
task cleanTransScriptParsers(overwrite : true) {}
task cleanTextParsers(overwrite : true) {}

// --- The tarball full distribution

task tarAll(type: Tar, dependsOn: ':targets:java:snapshot') {
	baseName 'tosca'
	classifier 'ALPHA'
	version '1.0'
	compression Compression.GZIP

	// Tosca compiler.
	into ('.') {
		from 'targets/java/dist/transscript-1.0.0-SNAPSHOT.jar'
		rename { String fileName -> 'tosca.jar'	}
	}

  // Java libraries
  into ('lib') {
      from 'targets/java/dist/lib'
      include '*.jar'
  }

	// Standard library
	into ('lib/std/') {
        from 'src/std/'
        include '**/*.tsc'
  }

  into ('lib') {
        from 'src/'
        include 'std.tsc'
  }

	// C++ Target
	into ('targets/cpp/runtime') {
		from 'targets/cpp/runtime/'
		include '*.*'
	}

	into ('targets/cpp') {
		from 'targets/cpp'
		include 'build.gradle'
	}

	into ('targets/cpp/resources') {
    from '.'
    include 'gradle/wrapper/**/*'
    include 'gradlew.*'
    include 'gradlew'
  }

	into ('targets/cpp/resources') {
		from 'targets/cpp/resources/'
		include '*.*'
	}

  into ('targets/cpp/std/src/std/') {
    from 'targets/cpp/std/src/std'
    include '*-extern*.*'
  }

	into ('.') {
		from '.'
		include 'VERSION'
		include 'LICENSE.TXT'
	}
}


// --- Local install

task install(type: Copy, dependsOn:'tarAll') {
    from tarTree(resources.gzip(tasks.getByPath(':tarAll').archivePath))
    into "${buildDir}/install"
}

// --- The Gradle wrapper.
task wrapper(type: Wrapper) {
    gradleVersion = '2.13'
}
